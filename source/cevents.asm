;EVENTS

;event_check()
;main SR for events
_ECHECK
	LDA S_EVENTS
	BEQ @RTS
	JSR _EVCHOOSE
	JSR _EPROCESS
	JSR _GX_CLRS
	JSR _EDRAW
@RTS
	RTS

;event_choose()
;picks the events for this WEEK
_EVCHOOSE
	JSR _ERESET
	LDA #EVENTMAX
	JSR _RNG ;0-3
	CLC
	ADC #$01
	STA FVAR1 ;this WEEK's event count
	
	LDA V_WEEK
	CMP V_EVBIG
	BEQ @MAJOR
	
	LDA #32
	JSR _RNG
	BNE @ZERO
	JMP @DONE ;1/32 chance for no events
@MAJOR
	LDA FVAR1
	CMP #01
	BNE @ZERO
	INC FVAR1 ;at least 2 events (1 is major)
@ZERO
	LDA FVAR1 ;event count
	STA V_EVENTC
@RNG
	LDA D_ECHANCE+7
	JSR _RNG
	
	LDX #$00
@CHNCELP
	INX
	CMP D_ECHANCE-1,X
	BCS @CHNCELP
	STX FVAR3 ;selected event index
	
	LDA V_WEEK
	CMP V_EVBIG
	BNE @MAJOR2
	LDA FVAR1
	CMP V_EVENTC
	BNE @MAJOR2
	
	LDA #EVENT_MAJOR
	JSR _RNG
	CLC
	ADC #EV_ANARCHY
	STA FVAR3
@MAJOR2
	LDY FVAR1
	LDA FVAR3
	STA V_EVENTS-1,Y
	DEC FVAR1
	LDA FVAR1
	BEQ @DONE
	JMP @RNG
@DONE
	RTS

;event_reset()
;resets the event list
_ERESET
	LDA #$00
	TAX
	STA V_EVENTC
@LOOP
	STA V_EVENTS,X
	INX
	CPX #$08
	BNE @LOOP
	TAX
	INX
@TABLOOP
	LDA V_ETABLE,X
	CMP #EV_ANARCHY ;major events permanent
	BCS @SKIP
	LDA #$00
	STA V_ETABLE,X
@SKIP
	INX
	CPX #STATE_C
	BNE @TABLOOP
	RTS

;event_process
;processes all events
_EPROCESS
	LDX #$00
	STX FAI ;event index
@LOOP
	LDX FAI
	LDA V_EVENTS,X
	CPX V_EVENTC
	BEQ @DONE
	ASL
	TAX
	LDA D_EADDR-2,X
	STA @EDIT+1
	LDA D_EADDR-1,X
	STA @EDIT+2
@EDIT
	JSR $0000
	INC FAI
	JMP @LOOP
@DONE
	RTS
	
;event_draw()
;draws event log
_EDRAW
	LDA S_SKIPGAME
	BEQ @SKG
	JMP @SKIPGAME
@SKG
	LDA #$00
	STA V_REDRW1
	JSR _DRWBORD2
	LDA #C_WHITE
	STA GX_DCOL
	
	+__COORD P_EVENTR,P_EVENTC
	+__LAB2XY T_EVINIT
	JSR _GX_STR
	LDA V_WEEK
	ORA #NUMBERS
	DEC GX_CCOL
	DEC GX_CCOL
	STA GX_CIND
	JSR _GX_CHAR
	
	INC GX_CROW
	INC GX_CROW
	INC GX_CROW
	LDA #P_EVENTC
	STA GX_CCOL
	
	LDA #$00
	STA FAI ;current event drawn
@LOOP	
	+__LAB2O T_EVENTS
	LDX FAI
	LDA V_EVENTS+4,X
	STA FSTATE
	AND #$0F
	STA GX_REG
	
	LDX FAI
	LDA V_EVENTS,X
	BNE @SKIP
@MAX
	JMP @RTS
@SKIP
	CPX V_EVENTC
	BEQ @MAX
	
	STA FAISUM ;event index (9-11 are checked for reuse)
	
	LDX FAISUM
	DEX
	LDY #19 ;event text length
	JSR _OFFSET
	+__O2XY
	JSR _GX_STR
	
	INC FAI
	INC GX_CROW
	INC GX_CROW
	LDA #P_EVENTC
	STA GX_CCOL
	
	JMP @LOOP
@RTS
	CPX #$00
	BNE @SOMETEXT
	+__LAB2XY T_EVNONE
	JSR _GX_STR
@SOMETEXT
	JSR _FTC
@SKIPGAME
	RTS

;event_state()
;assigns an event to a given state, not repeating
_ESTATE
	;1/2 chance to select megastate or medium state
	JSR _COINFLIP
	BEQ @RAND
	LDA #$00
	STA FARG3
	JSR _EECD2
	STA FSTATE
	JMP @SELECTED
@RAND
	JSR _RANDSTATE
@SELECTED	
	LDX #00
@LOOP
	LDA V_EVENTS+4,X
	BEQ @DONE
	CMP FSTATE
	BEQ _ESTATE
	INX
	CPX V_EVENTC
	BNE @LOOP
@DONE
	LDA FSTATE
	TAY
	LDA V_ETABLE,Y 
	BNE _ESTATE ;generate new state if it already had a (regional) event
	
	LDX FAI
	TYA
	STA V_EVENTS+4,X
	
	LDA V_EVENTS,X
	LDY FSTATE
	STA V_ETABLE,Y
	RTS

;event_region()
;assigns an event to a given region, not repeating, adding it to the event table
_EREGION
	JSR _EREGNT
	
	LDA V_EVENTS,X
	STA FVAR4 ;event index
	
	LDA FVAR1
	AND #$0F
	TAX
	LDA D_REGLIM-1,X
	STA FVAR2 ;lower state index
	LDA D_REGLIM,X
	STA FVAR3 ;upper state index
	
	LDX FVAR2
@REGLOOP
	LDA FVAR4
	STA V_ETABLE,X
	INX
	CPX FVAR3
	BNE @REGLOOP
	
	RTS
;above, but do not add event to event table
;event_region_no_table()
_EREGNT
	LDA #REGION_C
	JSR _RNG
	ORA #$F0
	STA FVAR1 ;region
	INC FVAR1
	
	LDX #00
@LOOP
	LDA V_EVENTS+4,X
	BEQ @DONE ;if zero, no more checking
	CMP FVAR1 ;no repeats
	BEQ _EREGNT
	CMP V_EVBIGR ;cannot choose major event region (which is locked)
	BEQ _EREGNT
	INX
	CPX V_EVENTC
	BNE @LOOP
@DONE
	LDA FVAR1
	LDX FAI
	STA V_EVENTS+4,X
	RTS

;any event not listed is simply marked in either the event variable list (permanent) or the event table (temporary)
;***COMMON EVENTS***
;event_state_moves_right()
_ESRIGHT
	JSR _ESTATE
	JSR _ECPOFFS

	LDY #$01+CPBLEAN ;REP + 1
	JSR _ECPINC
	
	LDA S_PLAYER
	CMP #$04
	BNE @ISSUE
	LDY #$02+CPBLEAN ;PAT + 1
	JSR _ECPINC
@ISSUE
_ESRIGHT2
	LDY #$00
@ISLOOP
	JSR _EISSINC
	INY
	CPY #ISSUEC
	BNE @ISLOOP
	
	RTS
;event_state_moves_left()
_ESLEFT
	JSR _ESTATE
	JSR _ECPOFFS
	LDY #$00+CPBLEAN ;DEM + 1
	JSR _ECPINC
	
	LDA S_PLAYER
	CMP #$04
	BNE @ISSUE
	LDY #$03+CPBLEAN ;SOC + 1
	JSR _ECPINC
@ISSUE
_ESLEFT2
	LDY #$00
@ISLOOP
	JSR _EISSDEC
	INY
	CPY #ISSUEC
	BNE @ISLOOP
	RTS
;event_state_deradicalizes()
_ESCENTER
	JSR _ESTATE
	JSR _ECPOFFS

	LDA S_GMMODE
	BNE @ISSUE
	LDA S_PLAYER
	CMP #$03 ;for 3 players, player 3 gets + 1
	BNE @EVEN
	LDY #$02+CPBLEAN
	JSR _ECPINC
	JMP @ISSUE
@EVEN ;for 2/4 players, move SL towards even
	LDA #01
	STA FVAR7
	JSR _SLEVEN
@ISSUE
_ESCENTER2
	LDY #$00
@ISLOOP
	JSR _EISSCEN
	INY
	CPY #ISSUEC
	BNE @ISLOOP
	RTS

;event_immigrate()
_EIMMIGRAT
	JSR _EREGNT
	JSR _EREGLIM
	
@LOOP
	LDX LOWSTATE
	LDA V_NEGLECT,X
	SEC
	SBC #02
	BCS @CAP
	LDA #00
@CAP
	STA V_NEGLECT,X
	
	INC V_EC,X
	INC LOWSTATE
	
	LDA #$01
	STA FARG2
	LDA LOWSTATE
	STA FARG3
	JSR _EECD
	
	LDA LOWSTATE
	CMP HIGHSTATE
	BNE @LOOP
	
	RTS

;***MAJOR EVENTS***
;all major events must store their region in V_EVBIG to avoid being reset!
_EANARCHY
	;pick a med/megastate
	LDA #$00
	STA FARG3
	JSR _EECD2
	LDX FAI
	STA V_EVENTS+4,X
	STA FSTATE ;hold
	
	JSR _ECPOFFS
	;set CP to 0
	LDY #00
	TYA
@CPLOOP
	STA (CP_ADDR),Y
	INY
	CPY S_PLAYER
	BNE @CPLOOP
	;equalize STATE LEAN
	LDY #CPBLEAN
@LEANLP
	LDA (CP_ADDR),Y
	BEQ @SKIP
	LDA #$07
	STA (CP_ADDR),Y
@SKIP
	INY
	CPY #CPBLOCK
	BNE @LEANLP
	;randomize ISSUES
	LDY #$00
@ISSLP
	LDA #$06
	JSR _RNG
	CLC
	ADC #$01
	STA (IS_ADDR),Y
	INY
	CPY #ISSUEC
	BNE @ISSLP
	;EC *= (0.75)
	LDX FSTATE
	LDA V_EC,X
	LSR
	LSR
	STA FVAR4
	LDA V_EC,X
	SEC
	SBC FVAR4
	STA V_EC,X
	;redistribute EC gains to any state (except the anarchy)
@DECLOOP
	LDA #STATE_C-1
	JSR _RNG
	TAX
	INX
	CPX FSTATE
	BEQ @DECLOOP
	INC V_EC,X
	
	DEC FVAR4
	LDA FVAR4
	BNE @DECLOOP
	
	RTS

_ETV
	JSR _EREGION
	JSR _EVENTBIG
	;do nothing
	RTS

_EVISIT
	JSR _EREGION
	JSR _EVENTBIG
	;do nothing
	RTS

;event_terror_attack
_ETERROR
	LDX FAI
	LDA V_EVENTS,X
	STA V_EVBIG
	
	JSR _CPOFFR
@LOOP
	LDA #01
	STA FVAR7
	JSR _SLEVEN
	
	JSR _CPOFFI
	BNE @LOOP
	
	RTS

;***HELPER FUNCTIONS***
_ECPINC
	LDA (CP_ADDR),Y
	CLC
	ADC #$01
	STA (CP_ADDR),Y
	RTS
_EISSINC
	LDA (IS_ADDR),Y
	CLC
	ADC #$01
	CMP #$07
	BEQ @RTS
	STA (IS_ADDR),Y
@RTS
	RTS
_EISSDEC
	LDA (IS_ADDR),Y
	SEC
	SBC #$01
	BEQ @RTS
	STA (IS_ADDR),Y
@RTS
	RTS
_EISSCEN
	LDA (IS_ADDR),Y
	CMP #$05
	BCS _EISSDEC
	CMP #$03
	BCC _EISSINC
	RTS
	
_ECPOFFS
	LDX FAI
	LDA V_EVENTS+4,X
	JSR _CPOFFS
	RTS

_EREGLIM
	LDX FAI
	LDA V_EVENTS+4,X
	AND #$0F
	TAX
	JSR _LREGLIM
	RTS

;marks the region of this event into the major event region variable	
_EVENTBIG
	LDX FAI
	LDA V_EVENTS+4,X
	STA V_EVBIG
	RTS

;event_ec_delta(FARG2 = inc/dec bool, FARG3 = ignore state)
;changes the EC of one mega/medstate
_EECD
	JSR _EECD2
	TAX
	LDA FARG2
	BNE @DEC
	INC V_EC,X
	RTS
@DEC
	DEC V_EC,X
	RTS
;picks a medium or megastate (FARG3 = state to not repeat)
_EECD2
	JSR _COINFLIP ;half chance for a mega or medium state
	BNE @MEGA
	
	JSR _RANDMEGAST
	CMP FARG3
	BEQ _EECD2
	JMP @CHOOSE
@MEGA
	JSR _RANDMEDSTA
	CMP FARG3
	BEQ _EECD2
@CHOOSE
	RTS

;event_on_in_state(A = event index)
;state index in Y
_EVENTON
	CMP V_ETABLE,Y
	RTS
	
;**************************************************************************
;CANDIDATE EVENTS
;**************************************************************************
_EVENTCAND
	LDA S_EVENTS
	BNE @RTS
	RTS
@RTS
	
	LDX V_PARTY
	LDA V_SCANDAL,X
@CHECKDEAD
	CMP #EVC_SCANDAL_DEATH
	BNE @CHECKMAJ
	LDA #01
	STA V_EVCDEAD
	BNE @SKIPCHOICE
@CHECKMAJ
	CMP #EVC_SCANDAL_MAJOR
	BNE @CHECKMIN
	LDA #00
	STA V_EVCDEAD
	LDA #01
	BNE @SKIPCHOICE
@CHECKMIN
	CMP #EVC_SCANDAL_MINOR
	BNE @GOOD
	LDA #00
	BEQ @SKIPCHOICE
@GOOD
	LDA #03
	JSR _RNG
	BNE @DONE
@REROLL
	LDA #EVCAND_C
	JSR _RNG
	CLC
	ADC #$02
@SKIPCHOICE	
	STA EVCI
	
	LDX V_PARTY
	LDA V_AI,X
	BEQ @AI
	LDA EVCI
	CMP #02 ;AI only processes death events
	BCS @DONE
@AI
	
	JSR _DRWCAND
	+__COORD P_MAP2R,P_FTCC
	+__LAB2XY T_EVENTC
	JSR _GX_STRA
	JSR _FTC

	LDA #$00
	STA V_REDRW1
	JSR _DRWBORD2
	
	+__COORD P_DBQR,P_DBQC
	+__LAB2XY T_EVENTC2
	JSR _GX_STR
	
	LDA EVCI
	+__LAB2XY D_EVC
	JSR _TABLJSR
	BEQ @REROLL
	
	JSR _EVCOVERF
	JSR _MAPCMB3
@DONE
	RTS
	
;candidate event outcomes (FARG1 = boolean YES/NO)
_EVCMINOR
	LDX V_PARTY
	LDA V_AI,X
	BNE @SKIPDRAW
	JSR _EVCDRAW
	JSR _FTC
@SKIPDRAW
	LDA #$FF
	JSR _EVCNATL
	JSR _EVCSCANCLR
	LDA #01
	RTS
	
_EVCMAJOR
	LDA V_EVCDEAD
	BEQ @SKIP
	LDA #EVCAND_C+2
	STA EVCI
@SKIP
	JSR _EVCDRAW
	LDA #EVCAND_C+3
	STA EVCI
	
	INC GX_CROW
	INC GX_CROW
	LDA #P_DBQC
	STA GX_CCOL
	
	LDX V_PARTY
	LDA V_AI,X
	BNE @SKIPDRAW
	JSR _EVCDRAW2
	JSR _FTC
@SKIPDRAW
	
	LDA #$FE
	JSR _EVCNATL
	
	LDA C_MONEY
	STA V_DEADHOLD+0
	LDA C_CER
	SEC
	SBC #02
	STA V_DEADHOLD+1
	LDA C_VP
	STA V_DEADHOLD+2
	
	JSR _CANDGEN
	JSR _CPYPRIM
	JSR _CANDSSC

	LDA V_DEADHOLD+0
	STA C_MONEY
	LDA V_DEADHOLD+1
	STA C_CER
	LDA V_DEADHOLD+2
	STA C_HOME

	JSR _EVCSCANCLR
	LDA #01
	RTS
	
_EVCCEO
	JSR _EVCCMB
	BEQ @NO

	LDA #$08
	STA C_CORP
	LDA C_FUND
	CLC
	ADC #$06
	STA C_FUND
	DEC C_CER
	INC C_TV
	INC C_TV
	JSR _EVCREDPILL
	JMP @DONE
@NO	
	DEC C_CORP
	DEC C_TV
	DEC C_FUND
@DONE
	LDA #01
	RTS
	
_EVCCHILD
	LDA C_MONEY
	CMP #60
	BCC @REROLL
	
	LDA #12
	JSR _RNG
	CLC
	ADC #20
	JSR _NEGATIV
	STA EVCCOST
	JSR _EVCCMBC
	BEQ @NO
	
	LDA C_MONEY
	CLC
	ADC EVCCOST
	STA C_MONEY
	INC C_CER
	JSR _EVCBLUEPILL
	JMP @DONE
@NO
	JSR _EVCSCANMINR
@DONE
	LDA #01
	RTS
@REROLL
	LDA #00
	RTS
	
_EVCDICT
	LDA #8
	JSR _RNG
	CLC
	ADC #25
	STA EVCCOST
	JSR _EVCCMBC
	BEQ @NO
	
	LDA C_MONEY
	CLC
	ADC EVCCOST
	BCC @OF
	LDA #$FF
@OF
	STA C_MONEY
	JSR _EVCSCANMINR
	JSR _EVCREDPILL
@NO
	LDA #01
	RTS
	
_EVCDIRT
	LDA C_MONEY
	CMP #40
	BCC @REROLL
	LDA #10
	JSR _RNG
	CLC
	ADC #25
	JSR _NEGATIV
	STA EVCCOST
	JSR _EVCDRAW
	JSR _EVCCOST
@REDRAW
	JSR _CLRBR
	JSR _EVCYESORNO
	BEQ @NO
	
	LDA C_MONEY
	CLC
	ADC EVCCOST
	STA C_MONEY
	
	JSR _DOMENU
	BEQ @REDRAW
	
	LDA #04
	JSR _RNG
	BEQ @FAILED
	TAX
	DEX
	LDA V_DOPPON,X
	TAX
	LDA #06
	JSR _EVCSCANDAL
@FAILED
	JSR _EVCSCANMINR
@NO
	LDA #01
	RTS
@REROLL
	LDA #00
	RTS
	
_EVCXTRME
	JSR _ISIND
	BNE @REROLL
	JSR _EVCCMB
	BEQ @NO
	LDA V_PARTY
	AND #%00000001
	BEQ @LEFT
	JSR _EVCREDPILL
	JMP @DONE
@LEFT
	JSR _EVCBLUEPILL
	JMP @DONE
@NO
	LDA #08
	JSR _EVCSCANMINR2
@DONE
	LDA #01
	RTS
@REROLL
	LDA #00
	RTS
	
_EVCFBI
	LDA V_WEEK
	CMP #09
	BEQ @REROLL
	
	LDA #REGION_C
	JSR _RNG
	TAX
	INX
	TXA
	STA GX_REG
	LDX V_PARTY
	STA V_RALLYDEATH,X
	
	JSR _EVCDRAW
	JSR _FTC
	
	LDA #01
	RTS
@REROLL
	LDA #00
	RTS
	
_EVCGLOW
	JSR _EVCREROLL
	BNE @REROLL
	
	LDA #10
	JSR _RNG
	CLC
	ADC #10
	JSR _NEGATIV
	STA EVCCOST
	JSR _EVCCMBC
	BEQ @NO
	LDA C_MONEY
	CLC
	ADC EVCCOST
	STA C_MONEY
	LDA #04
	JSR _RNG
	CLC
	ADC #02
	ADC C_TV
	STA C_TV
@NO
	LDA #01
	RTS
@REROLL
	LDA #00
	RTS
	
_EVCIVAN
	LDA #10
	JSR _RNG
	CLC
	ADC #05
	JSR _NEGATIV
	STA EVCCOST
	JSR _EVCCMBC
	BEQ @NO
	LDA C_MONEY
	CLC
	ADC EVCCOST
	BCS @OVER
	LDA #00
@OVER
	STA C_MONEY
@NO	
	LDA #01
	RTS
	
_EVCLOCK
	JSR _RANDSTATE
	LDA #20
	STA EVCCOST
	JSR _EVCCMBC
	BEQ @NO
	LDA C_MONEY
	CLC
	ADC #20
	STA C_MONEY
	INC C_FUND
	INC C_FUND
	
	LDX V_PARTY
	LDA FSTATE
	STA V_STLOCK,X
	
	JMP @DONE
@NO
	LDA FSTATE
	JSR _CPOFFS
	LDA V_PARTY
	CLC
	ADC #CPBLEAN
	TAY
	LDA (CP_ADDR),Y
	SEC
	SBC #01
	STA (CP_ADDR),Y
	JSR _EVCSCANMINR
@DONE
	LDA #01
	RTS
	
_EVCPILL
	JSR _EVCDRAW
	JSR _EVCREDORBLUE
	BEQ @RED
	JSR _EVCBLUEPILL
	JMP @DONE
@RED
	JSR _EVCREDPILL
@DONE
	LDA #01
	RTS
	
_EVCREAL
	JSR _EVCCMB
	BEQ @NO
	DEC C_CER
	DEC C_CER
	LDA C_FUND
	CLC
	ADC #04
	STA C_FUND
	DEC C_TV
	DEC C_TV
	
	LDA #01
	STA FSTATE
@HQLOOP
	LDY FSTATE
	JSR _HQPLUS
	INC FSTATE
	LDA FSTATE
	CMP #STATE_C
	BNE @HQLOOP
	JSR _EVCREDPILL
	JMP @DONE
@NO
	DEC C_CORP
	DEC C_FUND
	LDA C_TV
	CLC
	ADC #$05
	STA C_TV
@DONE	
	LDA #01
	RTS
	
_EVCSTAFF
	JSR _EVCREROLL
	BNE @REROLL
	LDA #09
	JSR _RNG
	CLC
	ADC #05
	JSR _NEGATIV
	STA EVCCOST
	JSR _EVCCMBC
	BEQ @NO
	
	LDA C_MONEY
	CLC
	ADC EVCCOST
	STA C_MONEY
	
	LDA #01
	LDX V_PARTY
	STA V_AUTOSTAFF,X
@NO	
	LDA #01
	RTS
@REROLL
	LDA #00
	RTS
	
_EVCVP
@REROLLST
	JSR _RANDSTATE
	CMP C_VP
	BEQ @REROLLST
	JSR _EVCCMB
	BEQ @NO
	LDY C_VP
	JSR _HQMINUS
	JSR _HQMINUS
	LDY FSTATE
	STY C_VP
	JSR _HQPLUS
	JSR _HQPLUS
	LDA FSTATE
	JSR _CPOFFS
	
	LDA #16
	STA FRET1
	JSR _CPADD
	
	JMP @DONE
@NO
	LDA #06
	JSR _EVCSCANMINR2
@DONE
	LDA #01
	RTS
	
_EVCWARP
	LDA V_WEEK
	CMP #06
	BCS @REROLL
	
	JSR _EVCCMB
	BEQ @NO
	LDX V_PARTY
	LDA #01
	STA V_WARP,X
	JSR _EVCSCANDEAD
	LDA #$02
	JSR _EVCNATL
@NO
	LDA #01
	RTS
@REROLL
	LDA #00
	RTS

;******************
;helper functions
;******************

;draws evc text
_EVCDRAW
	+__COORD P_DBQR+2,P_DBQC
_EVCDRAW2
	LDA EVCI
	ASL
	TAX
	LDA T_EVC,X
	PHA
	LDA T_EVC+1,X
	TAY
	PLA
	TAX
	LDA #C_WHITE
	STA GX_DCOL
	JSR _GX_STRA
	RTS

;applies national penalty (A = signed penalty)
_EVCNATL
	PHA
	LDX #00
	TXA
@CLRLOOP
	STA V_DNATL,X
	INX
	CPX #PLAYERMAX
	BNE @CLRLOOP
	PLA
	LDX V_PARTY
	STA V_DNATL,X
	JSR _DNATLA
	RTS

;displays cost of event 
_EVCCOST
	+__COORD P_EVCCOSTR,P_EVCCOSTC
	LDA EVCCOST
	AND #%10000000
	EOR #%10000000
	JSR _DRWSIGN
	LDA #VK_DOLLAR
	JSR _GX_STR2
	LDA EVCCOST
	BPL @NEG
	JSR _NEGATIV
@NEG
	TAX
	LDA #00
	JSR _INTCMB
	RTS

;combo
_EVCCMB
	JSR _EVCDRAW
	JSR _EVCYESORNO
	RTS
;with cost
_EVCCMBC
	JSR _EVCDRAW
	JSR _EVCCOST
	JSR _EVCYESORNO
	RTS

;evc_add_scandal(A = chance / 8, X = player index)
;adds scandal for current player, with A/8 chance to increment scandal counter. 
;if scandal counter is 1, a major scandal will occur if another is added.
_EVCSCANDAL
	STA FVAR1
	LDA #$08
	JSR _RNG
	CMP FVAR1
	BCC @CT
@RTS
	RTS
@CT
	LDA V_SCANDAL,X
	CMP #EVC_SCANDAL_MAJOR
	BCS @RTS
	TAY
	INY
	TYA
	JMP _EVCSCANDAL3
;call for candidate dying
_EVCSCANDAL2
	LDA #EVC_SCANDAL_DEATH
_EVCSCANDAL3
	STA V_SCANDAL,X
	RTS

;standard minor scandal
_EVCSCANMINR
	LDA #$01
_EVCSCANMINR2
	LDX V_PARTY
	JSR _EVCSCANDAL
	RTS
_EVCSCANDEAD
	LDX V_PARTY
	JSR _EVCSCANDAL2
	RTS

_EVCYESORNO
	JSR _EVCRESPONSE
	JSR _YESORNO
	RTS
_EVCREDORBLUE
	JSR _EVCRESPONSE
	+__COORD P_NOYESR,P_NOYESC
	+__LAB2XY T_REDBLUE
	JSR _YESORNO2
	RTS
	
_EVCRESPONSE
	+__COORD P_CONVNR-1,P_CONVNC
	+__LAB2XY T_EVENTC3
	JSR _GX_STR
	RTS

_EVCREDPILL
	LDX #00
@LOOP
	LDA C_ISSUES,X
	CMP #ISSUEX
	BEQ @SKIP
	CMP #ISSUEN
	BEQ @SKIP
	LDA #03
	JSR _RNG
	CLC
	ADC C_ISSUES,X
	CMP #08
	BCC @OVER
	LDA #ISSUEN
@OVER
	STA C_ISSUES,X
@SKIP
	INX
	CPX #ISSUEC
	BNE @LOOP
	JSR _CANDOTHR2
	RTS

_EVCBLUEPILL
	LDX #00
@LOOP
	LDA C_ISSUES,X
	CMP #ISSUEX
	BEQ @SKIP
	CMP #ISSUEN
	BEQ @SKIP
	LDA #03
	JSR _RNG
	STA FVAR1
	LDA C_ISSUES,X
	SEC
	SBC FVAR1
	BCS @OVER
	LDA #ISSUEN
@OVER
	STA C_ISSUES,X
@SKIP
	INX
	CPX #ISSUEC
	BNE @LOOP
	JSR _CANDOTHR2
	RTS

;reroll if funds < 20	
_EVCREROLL
	LDA C_MONEY
	CMP #20
	BCC @FAIL
	LDA #00
	RTS
@FAIL
	LDA #01
	RTS

;clears player scandal flag	
_EVCSCANCLR
	LDX V_PARTY
	LDA #00
	STA V_SCANDAL,X
	RTS

;kinda hacky, but sets any stats that are lower than 0 or negative to 1
_EVCOVERF
	LDX #00
@LP
	LDA C_CHAR,X
	BEQ @ONE
	CMP #80
	BCS @ONE
	BCC @SKIP
@ONE
	INC C_CHAR,X
@SKIP
	INX
	CPX #10
	BNE @LP
	RTS
	
	
	
	
	

