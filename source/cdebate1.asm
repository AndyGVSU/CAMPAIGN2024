;cdebate1.asm
;CDEBATE - DEBATE CODE

;checks if debate on, and runs if on
_DCHECK 
	LDA S_DEBTON
	BNE @ON
@RTS 
	RTS 
@ON 
	LDA V_PARTY
	BNE @RTS
	LDA V_WEEK
	CMP #$04
	BEQ @DEBWEEK
	CMP #$07
	BEQ @DEBWEEK
	BNE @RTS
@DEBWEEK 
	JSR _DMAIN
	RTS 

;main debate SR
_DMAIN 
	JSR _GX_CLRS
	JSR _DRWBORD2

	JSR _DNETWRK
	
	LDA S_SKIPGAME
	BNE @SKIP1
	JSR _DINTRO
	JSR _FTC

	JSR _MAPCMB1
	JSR _POPCOM1
	JSR _CANDLOOP
@SKIP1
	JSR _CANDOTHR
	JSR _DECSORT

	JSR _DQUESTN
	
	JSR _DNATL
	JSR _DLOG

	LDA #$00
	STA V_REDRW1
	LDA #$00
	STA V_PARTY
	JSR _MAPCMB2
	JSR _POPCOM1
	RTS 

;choose debate TV network
_DNETWRK 
	LDA #$03
	JSR _RNG
	STA V_DCTV
	LDA V_WEEK
	CMP #$07
	BNE @DONE
	LDA V_DPRVTV
	CMP V_DCTV
	BEQ _DNETWRK
@DONE 
	LDA V_DCTV
	STA V_DPRVTV
	RTS 



;debate_intro() 
_DINTRO 
	+__LAB2XY T_DINTRO
	+__COORD P_DINTRR,P_DINTRC
	JSR _GX_STR

	+__COORD P_DINTVR,P_DINTVC
	JSR _DDRWTV

	+__LAB2O T_DEBNUM
	LDA V_WEEK
	CMP #$04
	BEQ @DRWCT
	LDA OFFSET
	CLC 
	ADC #$05
	STA OFFSET
	BCC @DRWCT
	INC OFFSET+1
@DRWCT 
	+__COORD P_DINCTR,P_DINCTC
	+__O2XY
	JSR _GX_STR

	RTS 

;debate_draw_tv_network() 
_DDRWTV 
	+__LAB2O T_DEBNET
	LDX V_DCTV
	LDY #$05
	JSR _OFFSET
	+__O2XY
	JSR _GX_STR
	RTS 

;main question loop
_DQUESTN 
	LDA #$01
	STA V_DQUEST

	JSR _DINIT
@TOP 
	LDA #$00
	STA FPARTY
	STA V_PARTY
	JSR _DSTATE
	JSR _DNEXTQ
	JSR _DRESET

	+__LAB2O2 V_DACT
@ACTLOOP 
	LDA FPARTY
	JSR _CANDLOAD

	LDX FPARTY
	LDA V_AI,X
	BNE @AISKIP

	LDA #$00
	STA V_DPHASE
	JSR _DCLEAR

	JSR _DCLRMR

	LDA #00
	STA V_REDRW1
	JSR _DRWCAND

	+__COORD P_DPLYRR,P_DPLYRC
	LDA FPARTY
	JSR _DRWPLYR

	JSR _DACTSEL
	JMP @ACTDONE
@AISKIP
	JSR _DACTAI
@ACTDONE
	JSR _DACTINC
	INC FPARTY
	LDA FPARTY
	STA V_PARTY
	CMP S_PLAYER
	BNE @ACTLOOP

	LDA #$01
	STA V_DPHASE
	JSR _CLRBL
	JSR _DDRWRIN
	
	+__LAB2O2 V_DACT
	LDA #$00
	STA FPARTY ;reaction counter
@RCTLOOP
	;LDX FPARTY
	JSR _DRCTCHK
	BEQ @RCTDONE

	LDY #02
	LDA (OFFSET2),Y
	TAX
	LDA V_AI,X
	BNE @RCTAI

	JSR _DRCTSEL
	JMP @RCTDONE
@RCTAI
	JSR _DRCTAI
@RCTDONE
	JSR _DACTINC
	INC FPARTY
	LDA FPARTY
	CMP S_PLAYER
	BNE @RCTLOOP

	JSR _DQRESLT
	LDX V_DQUEST
	JSR _DADDLOG
	LDX V_DQUEST
	DEX 
	JSR _DPENLOG
	JSR _DAPLYDP
	JSR _DDPTOTL

	INC V_DQUEST
	LDA V_DQUEST
	CMP #$04
	BEQ @RTS
	JMP @TOP
@RTS 
	RTS 

;debate_action_block_increment() 
;move to next candidate's action schedule
_DACTINC 
	LDA OFFSET2
	CLC 
	ADC #$07
	STA OFFSET2
	BCC @CARRY
	INC OFFSET2+1
@CARRY 
	RTS 

;generate question state
_DSTATE 
	JSR _DSTATE2
	STA V_DQSTAT
	TAY 
	STA FARG1
	JSR _CPOFFS
	LDY #00
@CPYISS 
	LDA (IS_ADDR),Y
	STA V_DISSUE,Y
	INY 
	CPY #$05
	BNE @CPYISS

	LDX #$00
@TOP 
	LDA V_DCTV
	BNE @TV1
	LDA V_DISSUE,X
	CMP #$06
	BNE @ISSUE1
	LDA #$03
@DEC 
	DEC V_DISSUE,X
	BNE @ADJUST
@ISSUE1 
	CMP #$01
	BNE @ADJUST
	LDA #$03
@INC 
	INC V_DISSUE,X
	BNE @ADJUST
@TV1 
	CMP #$01
	BEQ @INC
	CMP #$02
	BEQ @DEC
@ADJUST 
	INX 
	CPX #$05
	BNE @TOP

	JSR _DSTATE4
	JSR _DSTATE3
	RTS 

;check picked state/question for repeats
_DSTATE2 
	JSR _DSTATE3
	
	CMP V_DSTATE+0
	BEQ _DSTATE2
	CMP V_DSTATE+1
	BEQ _DSTATE2
	CMP V_DSTATE+2
	BEQ _DSTATE2
	LDX V_DQUEST
	STA V_DSTATE-1,X
	RTS 
;pick question/state
_DSTATE3 
	LDA V_DQUEST
	CMP #$01 ;q1
	BNE @Q2

	LDA #$00
	STA OFFSET
	STA OFFSET+1
	LDX V_DCTV
	LDY #$03
	JSR _OFFSET

	LDA #$03
	JSR _RNG
	CLC 
	ADC OFFSET
	TAX 
	LDA D_TV2REG,X
	TAX 

	LDA D_REGLIM-1,X
	STA FVAR2
	LDA D_REGLIM,X
	STA FVAR3

@REROLL 
	LDA #STATE_C
	JSR _RNG
	CMP FVAR2
	BCC @REROLL
	CMP FVAR3
	BCS @REROLL
	RTS 
@Q2 
	CMP #$02
	BNE @Q3

	LDA #$09
	JSR _RNG
	TAX 
	LDA D_MEDSTA,X
	RTS 
@Q3 
	LDA #STATE_C
	JSR _RNG
	RTS 

;calculate "others" value for state
_DSTATE4 
	LDX #$00
@CPLOOP 
	LDA V_DISSUE,X
	STA V_STRING,X
	INX 
	CPX #$05
	BNE @CPLOOP
	JSR _AVGISSU
	LDA FRET1
	STA V_DISSUE+5
	RTS

;generate + draw next question
;LOCAL: FVAR1
_DNEXTQ
	LDA S_SKIPGAME
	BNE @REROLL
	
	JSR _GX_CLRS
	JSR _DRWBORD2
	+__COORD P_DBQR,P_DBQC
	+__LAB2XY T_QUEST
	JSR _GX_STR

	+__COORD P_DBQR,P_DBQNC

	LDA #C_WHITE
	STA GX_DCOL
	LDA V_DQUEST
	ORA #NUMBERS
	STA GX_CIND
	JSR _GX_CHAR

	+__COORD P_DBQR,P_DBQTC

	JSR _DDRWTV
	
@REROLL 
	LDA #$18
	JSR _RNG
	CMP V_DTOPIC+0
	BEQ @REROLL
	CMP V_DTOPIC+1
	BEQ @REROLL
	CMP V_DTOPIC+2
	BEQ @REROLL

	TAY 
	LDX V_DQUEST
	DEX 
	STA V_DTOPIC,X
	TAX 
	STA FVAR1

	LDA D_DTOPIC,Y
	TAX 
	AND #$0F
	STA V_DISS1
	TXA 
	LSR 
	LSR 
	LSR 
	LSR 
	STA V_DISS0

	LDA S_SKIPGAME
	BNE @RTS

	+__COORD P_DBQPR,P_DBQPC
	+__LAB2XY T_DQUEST
	JSR _GX_STR

	+__COORD P_DTOPR,P_DTOPC
	JSR _DRWTOPC

	+__COORD P_DBQPR,P_DBQPC
	LDA #C_YELLOW
	STA GX_DCOL
	LDA V_DQSTAT
	JSR _DRWPOST

	JSR _FTC
@RTS
	RTS 
;debate_draw_topic()
_DRWTOPC 
	+__LAB2O T_DTOPIC
	LDX V_DQUEST
	LDA V_DTOPIC-1,X
	TAX 
	LDY #$10
	JSR _OFFSET
	+__O2XY
	LDA #C_YELLOW
	STA GX_DCOL
	JSR _GX_STR
	RTS 

_DRWBORD2 
	JSR _DRWBORD
	LDA #C_BLUE
	STA GX_DCOL
	LDA #P_MAPC
	STA GX_LX1
	LDA #P_MAP2C
	STA GX_LX2
	LDA #P_MAPR
	STA GX_LY1
	LDA #P_MAP2R
	STA GX_LY2
	JSR _GX_RECT
	RTS 

;debate_action_select() 
;LOCAL: FVAR1,2
_DACTSEL 
	JSR _DSTATUS
	JSR _DAMENU
@BACK 
	JSR _DSMENU
	LDX #P_DAMR2
	LDY #P_DAMR3
	JSR _RSELECT
	LDY #$00
	STA (OFFSET2),Y



	LDY #00
	LDA (OFFSET2),Y
	CMP #DBPERSNL
	BEQ @NEXT2
	CMP #DBREST
	BNE @SKIPFTC
	JMP @FTC
@SKIPFTC 
	JSR _DIMENU
	BEQ @BACK
	SEC 
	SBC #$01
	LDY #$01
	STA (OFFSET2),Y

	LDY #$00
	LDA (OFFSET2),Y
	CMP #$08
	BNE @NEXT1
	JSR _DSURVEY ;new
	BEQ @BACK
@NEXT1 
	CMP #DBALLY
	BEQ @NEXT2
	CMP #DBDIFFER
	BEQ @NEXT2
	CMP #DBMORAL
	BEQ @NEXT2
	BNE @FTC
@NEXT2 
	JSR _CLRBR
	LDA #00
	TAX 
	JSR _DOMENU

	BNE @NEXT3
	JMP @BACK
@NEXT3 
	TAX 
	DEX 
	LDA V_DOPPON,X
	LDY #$02
	STA (OFFSET2),Y
@FTC 
	JSR _FTC
	BEQ @RTS
	JMP @BACK
@RTS 
	RTS 