;cmusic.asm
;plays themes!

;play_music()
;plays an SID file
_MUSIC
	JSR _PTMUSIC
	LDA #$D0
	STA V_MUSTMR

	SEI
	LDA #<@IRQ
	LDX #>@IRQ
	STA INT_ADDR
	STX INT_ADDR+1
	LDA #$1B
	LDX #$00
	LDY #$7F 
	STA $D011
	STX $D012
	STY $DC0D
	LDA #$01
	STA $D01A
	STA $D019 ; ACK ANY RASTER IRQS
	LDA #$00
	LDX #$00
	JSR $0000 ;placeholder!!!
	CLI
@HOLD         
	JMP @HOLD ;WE DON'T WANT TO DO ANYTHING ELSE HERE. :)
	; WE COULD ALSO RTS HERE, WHEN ALSO CHANGING $EA81 TO $EA31
@IRQ
	LDA #$01
	STA $D019 ; ACK ANY RASTER IRQS
	LDX #$03
	JSR $0000 ;placeholder!!!
	
	DEC V_MUSTMR
	LDA V_MUSTMR
	BEQ @DONE
	JMP $EA31
@DONE
	;JSR _GX_ECM
	LDA #<INTERRUPT
	STA INT_ADDR
	LDA #>INTERRUPT
	STA INT_ADDR+1
	
	LDA #$00
	STA $D01A
	PLA
	PLA
	PLA
	PLA
	PLA
	PLA
	JSR _RNGINIT
	RTS
	
_PTMUSIC
	+__LAB2O _MUSICBASE
	LDA #$00
	TAY
@CLRLOOP
	STA (OFFSET),Y ;clear song area
	INY
	CPY #SONGSIZE_MAX
	BNE @CLRLOOP
	
	JSR _MUSICGL
	STA FARG5
	;SELF-MODIFYING CODE: changes _MUSIC to play the chosen label
	STX FARG1
	STY FARG2
	+__LAB2XY _MUSICBASE
	STX FARG3
	STY FARG4
	JSR _COPY
	
	+__LAB2XY D_MUSIC
	STX _MUSIC+47
	STY _MUSIC+48
	
	+__LAB2XY D_MUSIC+3
	STX _MUSIC+61
	STY _MUSIC+62
	
	RTS
	
;music_get_label()
;sets address to X/Y
_MUSICGL
	LDA V_PARTY
	BEQ @DEM
	CMP #$01
	BEQ @REP
	CMP #$03
	BEQ @SOC
	LDA S_PLAYER
	CMP #$04
	BEQ @PAT
	LDA S_3PMODE
	BEQ @IND
@SOC
	+__LAB2XY D_MUSS
	LDA _MUSICSIZE+3
	JMP @NEXT
@IND
	+__LAB2XY D_MUSI
	LDA _MUSICSIZE+4
	JMP @NEXT
@PAT
	+__LAB2XY D_MUSP
	LDA _MUSICSIZE+2
	JMP @NEXT
@REP
	+__LAB2XY D_MUSR
	LDA _MUSICSIZE+1
	JMP @NEXT
@DEM
	+__LAB2XY D_MUSD
	LDA _MUSICSIZE+0
@NEXT
	RTS