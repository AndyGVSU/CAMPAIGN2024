;cdebate2.asm
;DEBATE2 

;debate_opponent_menu_draw() 
_DOMENU 
	JSR _DSMENU
@PTLOOP 
	CMP V_PARTY
	BEQ @NEXTPT
	STA V_DOPPON,X
	INX 
@NEXTPT 
	CLC 
	ADC #$01
	CMP S_PLAYER
	BNE @PTLOOP

	LDX S_PLAYER
	DEX 
	STX FVAR1 ;player count - 1

	LDA V_PARTY
	STA FVAR2 ;current party

	LDA #00
	STA FVAR3
@OPPLOOP 
	LDA FVAR3
	CLC 
	ADC #P_DOPPR2
	STA GX_CROW
	LDA #P_DOPPC
	STA GX_CCOL

	LDA FVAR3
	TAX 
	LDA V_DOPPON,X
	STA V_PARTY
	JSR _DRWPLYR
	INC FVAR3
	LDA FVAR3
	CMP FVAR1
	BNE @OPPLOOP

	LDA FVAR2
	STA V_PARTY ;reload party

	DEC FVAR3
	LDA FVAR3
	CLC 
	ADC #P_DOPPR2
	TAY 
	LDX #P_DOPPR1
	JSR _RSELECT

	RTS 

;debate_action_menu_draw() 
_DAMENU 
	LDA #C_WHITE


	STA GX_DCOL
	+__COORD P_DAMR,P_DAMC
	+__LAB2XY T_DAMENU
	JSR _GX_STR
	RTS 
;debate_secondary_menu_draw() 
_DSMENU 
	JSR _CLRBR
	+__COORD P_DSMR,P_DSMC
	+__LAB2XY T_BACK
	JSR _GX_STR
	RTS 
;debate_issue_menu_draw() 
_DIMENU 
	+__COORD P_DIMR2,P_DIMC
	+__LAB2XY T_ISSUES
	JSR _GX_STR
	LDX #P_DIMR1
	LDY #P_DIMR3
	JSR _RSELECT
	RTS 
;debate_survey() 
;checks for issue conformity and displays
_DSURVEY 
	LDA C_MONEY
	CMP #$05
	BCS @NOMONEY
	LDA #$00
	RTS 
@NOMONEY 
	SEC 
	SBC #$05
	STA C_MONEY

	LDX V_PARTY
	JSR _DPENINC
	LDA #$01
	STA V_DPPREV,X

	+__COORD P_DCONFR,P_DCONFC

	LDY #$01
	LDA (OFFSET2),Y
	TAX 
	LDY V_PARTY
	JSR _DBIC
	BEQ @NONCONF

	+__LAB2XY T_DCONF
	JMP @DRAW
@NONCONF 
	+__LAB2XY T_DNCONF
@DRAW 
	JSR _GX_STR

	JSR _DRWFUND

	JSR _FTC
	LDA #00
	RTS 

;debate_issue_conformity(X = issue index; Y = player index)
;returns A = boolean value (issue match)
;checks if issue is off-by-one or exact match
_DBIC
	JSR _DLCISS
	SEC 
	SBC V_DISSUE,X
_DBIC2
	BPL @NEG
	CMP #$FF
@NEG 
	BEQ @TRUE
	CMP #$01
	BEQ @TRUE
	BNE @FALSE
@TRUE 
	LDA #$01
	RTS
@FALSE
	LDA #00
	RTS 

;debate_ai_issue_conformity(X = issue index; Y = player index)
;returns boolean match
;always uses CULTR (+ TV network)
_DBICAI 
	JSR _DLCISS
	SEC 
	SBC V_DISSUE+1
	JSR _DBIC2
	RTS
	
;debate_load_candidate_issue(X = issue index; Y = player index)
;LOCAL: FVAR3
;A = returned issue value
_DLCISS 
	CPX #$05
	BNE @NOTOTHER
	LDA V_DOTHER,Y
	RTS
@NOTOTHER

	TXA
	PHA ;save issue index
	STY FVAR3
	CPY #$00
	BEQ @LOAD
@LOOP 
	CLC
	ADC #CANDDATA
	DEC FVAR3
	LDX FVAR3
	BNE @LOOP
@LOAD
	TAX
	LDA V_ALLCND+25,X
	STA FVAR3
	PLA 
	TAX 
	LDA FVAR3
	RTS 

;debate_draw_status() 
_DSTATUS 
	LDA V_DPHASE
	BEQ @ACTION

	RTS 
@ACTION 
	+__COORD P_DSTATR,P_DSTATC
	LDX V_PARTY
	LDA V_DPOBSC,X
	BEQ @SKIPPEN
@BLUNDER 
	+__LAB2XY T_STATP
	JMP @DRAW
@SKIPPEN 
	LDA V_DPPREV,X
	BEQ @SKIPPEN2
	JMP @BLUNDER
@SKIPPEN2 
	LDA V_DDP,X
	BMI @TROUBLE
	+__LAB2XY T_STATOK
	JMP @DRAW
@TROUBLE 
	+__LAB2XY T_STATTR
@DRAW 
	JSR _GX_STR
	RTS 

;debate_clear_screen() 
_DCLEAR 
	JSR _CLRBL
	JSR _CLRBR
	JSR _DCLRMR
	JSR _DUPNEXT
	
	JSR _FTC
	RTS 

_DUPNEXT
	+__LAB2XY T_UPNEXT
	+__COORD P_UPNEXR,P_UPNEXC
	JSR _GX_STR

	LDX V_PARTY
	JSR _DRWPN1
	RTS

;debate_draw_reaction() 
;draws reaction intro text
_DDRWRIN 
	+__LAB2XY T_REPLY
	+__COORD P_REACTR,P_REACTC
	JSR _GX_STR
	RTS 

;debate_check_for_reaction()
;checks if an action prompts a reaction
_DRCTCHK
	LDY #00
	LDA (OFFSET2),Y
	JSR _DAISATK ;new
	RTS

;debate_reaction_select() 
_DRCTSEL 
@BACK 
	JSR _DCLRMR

	+__COORD P_DATTOR,P_DATTOC

	LDY #$02
	LDA (OFFSET2),Y
	STA V_PARTY ;defender
	JSR _DRWPLYR

	+__COORD P_DATTR,P_DATTC
	+__LAB2XY T_ATTACK
	JSR _GX_STR

	+__COORD P_DATTAR,P_DATTAC
	LDA FPARTY
	STA V_PARTY ;attacker
	JSR _DRWPLYR

	JSR _FTC

	LDY #$02
	LDA (OFFSET2),Y
	STA V_PARTY ;defender
	JSR _CANDLOAD
	LDA #00
	STA V_REDRW1
	JSR _DRWCAND

	JSR _DSMENU
	+__COORD P_DRMENR,P_DRMENC
	+__LAB2XY T_DRMENU
	JSR _GX_STR

	LDX #P_DRMENR
	LDY #P_DRMEN2
	JSR _RSELECT

	LDY #$04
	STA (OFFSET2),Y
	CMP #$01
	BNE @FTC

	JSR _DIMENU
	BNE @SELECT
	JMP @BACK
@SELECT 
	SEC 
	SBC #$01
	LDY #$05
	STA (OFFSET2),Y
@FTC 
	JSR _FTC
	BEQ @CONFIRM
	JMP @BACK
@CONFIRM 
	RTS 

;debate_turn_reset() 
_DRESET 
	LDX #00
	LDA #$FF
@LOOP 
	STA V_DACT,X
	INX 
	CPX #$1B
	BNE @LOOP

	LDX #00
@LOOP2 
	LDA #00
	STA V_DPOBSC,X
	LDA V_DPCURR,X
	STA V_DPPREV,X

	LDA #00
	STA V_DPCURR,X
	INX 
	CPX S_PLAYER
	BNE @LOOP2
	RTS 

;debate_action_is_attack() 
_DAISATK 
	CMP #DBDIFFER
	BEQ @TRUE
	CMP #DBMORAL
	BEQ @TRUE
	CMP #DBPERSNL
	BEQ @TRUE
	LDA #00
	RTS 
@TRUE 
	LDA #$01
	RTS

;debate_log_view()
_DLOG
	LDA S_SKIPGAME
	BNE @RTS
	
	JSR _DSMENU
	JSR _DFINAL

	+__COORD P_DLOGMR,P_DLOGMC
	+__LAB2XY T_DLOGM
	JSR _GX_STR
	LDX #P_DLOGMR2
	LDY #P_DLOGMR3
	JSR _RSELECT
	BEQ @VIEWLOG
@RTS
	RTS 
@VIEWLOG 
	LDA #00
	STA FPARTY
@LOOP 
	JSR _DDRWLOG
	INC FPARTY
	LDA FPARTY
	CMP S_PLAYER
	BNE @LOOP
	BEQ _DLOG

;debate_draw_final_results()
_DFINAL
	JSR _DRWBORD2
	
	+__COORD P_DFINR,P_DFINC
	+__LAB2XY T_DFINAL
	JSR _GX_STR
	
	+__COORD P_DFINR2,P_DFINC
	+__LAB2XY T_UNFAV
	JSR _GX_STR
	
	JSR _DFAVOR
	RTS

;debate_draw_favorables()
_DFAVOR
	LDA #00
	STA FPARTY
@TOP
	LDA FPARTY
	CLC
	ADC #P_DFINR3
	
	STA GX_CROW
	STA FVAR6
	LDA #P_DFINC
	STA GX_CCOL
	
	LDA FPARTY
	STA V_PARTY
	TAX
	LDA V_DDP,X
	CLC
	ADC #$24
	STA FARG2
	LDA #$48
	STA FARG4
	LDA #$00
	STA FARG1
	STA FARG3
	
	LDA FPARTY
	JSR _DRWPLYR
	
	LDA #P_DFINC2
	STA GX_CCOL
	
	JSR _PERCENT
	+__LAB2XY V_FPOINT
	JSR _GX_STR
	
	JSR _DRWSPAC
	
	LDA #$48
	SEC
	SBC FARG2
	STA FARG2
	
	JSR _PERCENT
	+__LAB2XY V_FPOINT
	JSR _GX_STR
	
	JSR _DRWSPAC
	JSR _DDRWNAT
	
	INC FPARTY
	LDA FPARTY
	CMP S_PLAYER
	BNE @TOP
RTS
	
;debate_draw_national_bonus()
_DDRWNAT
	LDA #VK_SPACE
	JSR _GX_CHAR
	
	LDX FPARTY
	LDA V_DNATL,X
	BMI @NEG
	STA FARG1,X
	LDA #$01
	BNE @DRAW
@NEG
	EOR #$FF
	CLC
	ADC #$01
	STA FARG1,X
	LDA #00
@DRAW
	JSR _DRWSIGN
	
	LDX FPARTY
	LDA FARG1,X
	TAX
	LDA #00
	JSR _INT
	JSR _GX_STR
	
	RTS

;draw_sign(A = success value)
;draws plus '+' or minus '-' sign	
_DRWSIGN
	BEQ @POS
	LDA #C_LGREEN
	STA GX_DCOL
	LDA #$2B
	BNE @DRAW
@POS
	LDA #C_PINK
	STA GX_DCOL
	LDA #$2D
@DRAW
	STA GX_CIND
	JSR _GX_CHAR
	INC GX_CCOL
	RTS

;debate_national_bonus()
_DNATL
	JSR _DNATLC
	JSR _DNATLA
	RTS
	
;debate_national_bonus_calc()
_DNATLC
	JSR _MAXR
	LDX #00
	LDY #00
@COPY
	LDA V_DDP,X
	CLC
	ADC #$24
	STA V_MAX,Y
	INX
	INY
	INY
	CPX S_PLAYER
	BNE @COPY
	
	JSR _MAX2
	BEQ @TIE
	TAX
	DEX
	LDA #$02
	STA V_DNATL,X
	JMP @DONE
@TIE
	LDX #00
@MAXLOOP
	LDA V_DDP,X
	CLC
	ADC #$24
	CMP FRET1
	BNE @NOTMAX
	LDA #$02
	STA V_DNATL,X
@NOTMAX
	INX
	CPX S_PLAYER
	BNE @MAXLOOP
@DONE ;?

	LDY #00
@PENLOOP
	LDA #00
	STA FVAR6 ;penalty count
	LDA V_DPCOUN,Y
@MOREPEN
	SEC
	SBC #$03
	BMI @NOPEN
	INC FVAR6
	BNE @MOREPEN
@NOPEN
	LDA V_DNATL,Y
	SEC
	SBC FVAR6
	STA V_DNATL,Y
	
	INY
	CPY S_PLAYER
	BNE @PENLOOP
RTS

;debate_national_bonus_apply()
_DNATLA
	LDA #$01
	STA FSTATE
@STLOOP
	LDA FSTATE
	STA FARG1
	JSR _CPOFFS
	LDX #00
@PTLOOP
	INX 
	TXA
	TAY
	LDA V_DNATL-1,X
	STA FVAR6 ;national bonus value
	
	TXA
	TAY
	STY FY1
	LDA (CP_ADDR),Y
	STA FSUS2
	LDA FVAR6
	JSR _ADDSUS
	LDA FSUS1
	LDY FY1
	STA (CP_ADDR),Y
	
	CPX S_PLAYER
	BNE @PTLOOP
	
	INC FSTATE
	LDA FSTATE
	CMP #STATE_C
	BNE @STLOOP

	JSR _STCTRL

	LDA #00
	STA V_PARTY
	JSR _CANDLOAD
	RTS

;debate_draw_question_result() 
_DQRESLT
	JSR _GX_CLRS
	JSR _DQRES2
@ACTLOOP 
	LDA #00
	STA V_DPCQAG
	STA V_DPCQDG
	
	LDY #00
	LDA (OFFSET2),Y
	STA FVAR5 ;stored action code
	JSR _DAISATK
	BNE @ISACT
	;JSR _DDEBUGS1
	JSR _DACTEXE
	JSR _DDPADD
@ISACT
	JSR _DQRES3
	BNE @ACTLOOP
	
	JSR _DQRES2
@RCTLOOP
	LDA #00
	STA V_DPCQAG
	STA V_DPCQDG
	
	LDY #$04
	LDA (OFFSET2),Y
	CMP #$FF
	BEQ @ISREACT
	;JSR _DDEBUGS2
	JSR _DRCTEXE
	JSR _DDPADD
@ISREACT

	JSR _DQRES3
	BNE @RCTLOOP

	JSR _DQRES2
@CLEAR
	LDA S_SKIPGAME
	BNE @SKIPG
	
	JSR _DRWBORD2

	+__COORD P_DQAR,P_DQAC
	JSR _DDRWACT

	+__COORD P_DQRR,P_DQRC
	JSR _DDRWRCT

	JSR _DDRWAUD
	JSR _FTC
@SKIPG
	JSR _DQRES3
	BNE @CLEAR
	
	;JSR _DDEBUGL
	RTS 
;debate_result_reset 
_DQRES2 
	LDA #00
	STA FPARTY
	+__LAB2O2 V_DACT
	RTS 
;debate_result_loop_inc 
_DQRES3 
	JSR _DACTINC
	INC FPARTY
	LDA FPARTY
	CMP S_PLAYER
	RTS 

;debate_turn_result_draw_action() 
_DDRWACT 
	+__LAB2O T_DRESLT

	LDY #00
	LDA (OFFSET2),Y
	JSR _DDRWRES

	+__COORD P_DQAPR,P_DQAPC

	LDA FPARTY
	STA V_PARTY
	JSR _DRWPLYR

	JSR _DDRWAC2
	RTS 
;debate_draw_action_fillin() 
_DDRWAC2 
	LDY #00
	LDA (OFFSET2),Y

	CMP #DBPERSNL
	BNE @MORAL

	+__COORD P_DQF6R,P_DQF6C
	JSR _DDRWAC3
	RTS 
@MORAL 
	CMP #DBMORAL
	BNE @DIFFER

	+__COORD P_DQF5R,P_DQF5C
	JSR _DDRWAC3
	+__COORD P_DQF5S,P_DQF5D
	JSR _DDRWAC4
	RTS 
@DIFFER 
	CMP #DBDIFFER
	BNE @ALLY
@DRDIFF
	+__COORD P_DQF4R,P_DQF4C
	JSR _DDRWAC3
	+__COORD P_DQF4S,P_DQF4D
	JSR _DDRWAC4
	RTS 
@ALLY 
	CMP #DBALLY
	BNE @CHALL
	BEQ @DRDIFF
@CHALL 
	CMP #DBCHALL
	BNE @ANSWER
@DRCHALL
	+__COORD P_DQF2R,P_DQF2C
	JSR _DDRWAC4
	RTS 
@ANSWER 
	CMP #DBANSWER
	BNE @PIVOT
	BEQ @DRCHALL
@PIVOT 
	+__COORD P_DQF0R,P_DQF0C
	JSR _DDRWAC4
	RTS 
_DDRWAC3 
	LDY #02
	LDA (OFFSET2),Y
	STA V_PARTY
	JSR _DRWPLYR
	RTS 
_DDRWAC4 
	LDY #01
	LDA (OFFSET2),Y
	JSR _DDRWISS
	RTS 

;debate_turn_result_draw_reaction() 
;LOCAL: FVAR5
_DDRWRCT 
	+__LAB2O T_DRESLT

	LDY #$04
	LDA (OFFSET2),Y
	STA FVAR5
	CMP #$FF
	BEQ @RTS

	CLC 
	ADC #$08

	JSR _DDRWRES

	+__COORD P_DQRPR,P_DQRPC
	JSR _DDRWAC3

	LDA FVAR5
	CMP #DBPIVR
	BNE @RTS

	+__COORD P_DQF7R,P_DQF7C
	LDY #$05
	LDA (OFFSET2),Y
	JSR _DDRWISS
@RTS 
	RTS 

;debate_turn_result_draw(A = act/rct index)
_DDRWRES 
	TAX
	LDA D_DRESLT,X
	CLC
	ADC OFFSET
	STA OFFSET
	BCC @CARRY
	INC OFFSET+1
@CARRY 

	+__O2XY
	JSR _GX_STR
	RTS 

;debate_draw_issue(A = issue index)
_DDRWISS
	TAX
	+__LAB2O T_ISSUES+1
	
	LDY #$06
	JSR _OFFSET
	
	LDY #00
	TAX
@DRWLOOP
	LDA (OFFSET),Y
	STA V_STRING,Y
	INX
	INY
	CPY #05
	BNE @DRWLOOP
	
	LDA #00
	STA V_STRING+5
	LDA #C_BLUE
	STA GX_DCOL
	+__LAB2XY V_STRING
	JSR _GX_STR
	RTS

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;debate_action_execute()
_DACTEXE
	
	LDA FVAR5
	CMP #DBREST
	BNE @ALLY
	LDX FPARTY
	LDA #$FE
	JSR _DDPADDA2
	JSR _DPENRM
	RTS
@ALLY	
	CMP #DBALLY
	BNE @CHALL
	
	LDY #02
	LDA (OFFSET2),Y
	STA FVAR6 ;store ally opponent
	LDY #01
	LDA (OFFSET2),Y
	STA FVAR4 ;store ally issue
	JSR _DBCS
	BEQ @ALLYFAIL
	
	LDY #01
	LDA (OFFSET2),Y
	TAX
	LDY FPARTY
	JSR _DBIC
	BEQ @ALLYFAIL
	
	LDY #01
	LDA (OFFSET2),Y
	TAX
	LDY FVAR6
	JSR _DBIC
	BEQ @ALLYFAIL
	;SUCCESS
	LDA #DBALAS
	JSR _DDPADDA ;self
	LDA #DBALDS
	LDX FPARTY
	JSR _DDPADDD ;opponent
	RTS
@ALLYFAIL
	LDA #$FE
	LDA #DBALAF
	JSR _DDPADDA
	LDA #DBALDF
	JSR _DDPADDD
	RTS
@CHALL
	CMP #DBCHALL
	BNE @ANSWER
	
	LDY #$01
	LDA (OFFSET2),Y
	JSR _DBCS
	BNE @CHALLNS
	LDA #DBCHF2
	JSR _DDPADDA
	RTS
@CHALLNS
	LDY #$01
	LDA (OFFSET2),Y
	TAX
	LDY FPARTY
	JSR _DBIC
	BEQ @CHALLIC
	
	LDA #DBCHF
	JSR _DDPADDA
	RTS
@CHALLIC
	LDA #DBCHS
	JSR _DDPADDA
	RTS
	
@ANSWER
	CMP #DBANSWER
	BNE @PIVOT

	LDY #$01
	LDA (OFFSET2),Y
	JSR _DBCS
	BEQ @ANSCSF
	LDA #DBANS
	BNE @ANS1
@ANSCSF
	LDA #DBANF
@ANS1
	JSR _DDPADDA
	
	LDY #$01
	LDA (OFFSET2),Y
	TAX
	LDY FPARTY
	JSR _DBIC
	BEQ @ANSICF
	LDA #DBANS
	BNE @ANS2
@ANSICF
	LDA #DBANF
@ANS2
	JSR _DDPADDA
	RTS
@PIVOT
	LDY #$01
	LDA (OFFSET2),Y
	JSR _DBCS
	BEQ @PIVNS
	LDA #DBPIF2
	JSR _DDPADDA
	RTS
@PIVNS
	LDY #01
	LDA (OFFSET2),Y
	TAX
	LDY FPARTY
	JSR _DBIC
	BNE @PIVNC
	LDA #DBPIF
	JMP @PIVAPPLY
@PIVNC
	LDA #DBPIS
@PIVAPPLY
	JSR _DDPADDA
	RTS

;debate_reaction_execute()
_DRCTEXE
	;LDY #$02
	;LDA (OFFSET2),Y
	;STA V_PARTY
	;JSR _DRWPLYR
	
	LDA #$01
	STA FVAR6 ;store success (default true)
	
	LDY #00
	LDA (OFFSET2),Y
	STA FVAR5 ;store action code

;ATTACKER ACTION
	CMP #DBPERSNL
	BNE @MORLDIFF
	JSR _DRCTEX3
	JMP @DEFEND
@MORLDIFF
	JSR _DRCTEX2
	JMP @DEFEND
	
;DEFENDER REACTION
@DEFEND
	LDA FVAR6 ;store attack success
	LDY #$03
	STA (OFFSET2),Y

	LDY #$04
	LDA (OFFSET2),Y
	
	CMP #DBCNTR
	BNE @PIVOT
	JSR _DRCTEX4
	RTS
@PIVOT
	CMP #DBPIVR
	BNE @ACCEPT
	JSR _DRCTEX5
	RTS
@ACCEPT
	JSR _DRCTEX6
	RTS

;moralize/differ
_DRCTEX2
	CMP #DBDIFFER
	BEQ @DIFFER
	
	LDY #$01
	LDA (OFFSET2),Y
	TAX
	LDY FPARTY
	JSR _DBIC
	BEQ @FAILURE
@DIFFER	
	LDY #$01
	LDA (OFFSET2),Y
	JSR _DBCS
	BEQ @FAILURE
	
	LDY #$01
	LDA (OFFSET2),Y
	TAX
	LDY #$02
	LDA (OFFSET2),Y
	TAY
	JSR _DBIC
	BNE @FAILURE
	RTS ;action success
@FAILURE
	LDA #00
	STA FVAR6
	RTS
;personal
_DRCTEX3
	LDA V_DQUEST
	CMP #$01
	BEQ @SKIPLOG

	LDX V_DQUEST
	LDY FPARTY
	JSR _DGETLOG ;new
	LDY #00
	LDA (OFFSET),Y
	CMP #DBPERSNL
	BEQ @FAILURE
@SKIPLOG
	LDX FPARTY
	LDA V_DPPREV,X
	BNE @FAILURE
	LDX V_PARTY 
	LDA V_DPPREV,X
	BEQ @FAILURE
	RTS	;success
@FAILURE
	LDA #00
	STA FVAR6
	RTS
;counter
_DRCTEX4
	LDA FVAR6
	BEQ @FAILURE
	
	LDA #DBCODF
	JSR _DDPADDD
	LDA #DBCOAS
	JSR _DDPADDA
	;JSR _DASUCC
	JSR _DDFAIL
	JMP @EXTRA
@FAILURE
	LDA #DBCODS
	JSR _DDPADDD
	LDA #DBCOAF
	JSR _DDPADDA
	;JSR _DAFAIL
	JSR _DDSUCC
@EXTRA	
	JSR _DRCTEX7
	RTS
;pivot
_DRCTEX5
	
	LDY #$05
	LDA (OFFSET2),Y
	JSR _DBCS
	BNE @FAILURE
	
	LDY #$05
	LDA (OFFSET2),Y
	TAX
	LDY #$02
	LDA (OFFSET2),Y
	TAY
	JSR _DBIC
	BEQ @FAILURE
	
	;success
	LDA #DBPIAS
	JSR _DDPADDA
	LDA #DBPIDS
	JSR _DDPADDD
	;JSR _DASUCC
	JSR _DDSUCC
	JMP @EXTRA
@FAILURE
	LDA #DBPIAS
	JSR _DDPADDA
	LDA #DBPIDF
	JSR _DDPADDD
	;JSR _DASUCC
	JSR _DDFAIL
@EXTRA
	JSR _DRCTEX7
	RTS

;accept
_DRCTEX6
	LDA FVAR6
	BEQ @FAILURE
	
	LDA #DBACAS
	JSR _DDPADDA
	LDA #DBACDS
	JSR _DDPADDD2
	;JSR _DASUCC
	JSR _DDSUCC
	JMP @EXTRA
@FAILURE
	LDA #DBACAS
	JSR _DDPADDA
	LDA #DBACDF
	JSR _DDPADDD
	;JSR _DASUCC
	JSR _DDFAIL
	JSR _DRCTEX7
@EXTRA
	RTS
;debate extra reaction processing
_DRCTEX7
	LDA FVAR5
	CMP #DBDIFFER
	BNE @NOTDIFF

	LDA V_DPCQAG
	JSR _DIV2S
	STA V_DPCQAG
	LDA V_DPCQDG
	JSR _DIV2S
	STA V_DPCQDG
@NOTDIFF
	LDA FVAR5
	CMP #DBPERSNL
	BNE @RTS
	
	LDA FVAR6
	BEQ @ADDPEN
	
	LDY #$02
	LDA (OFFSET2),Y
	TAX
	JSR _DPENADD
	RTS
@ADDPEN
	LDX FPARTY
	JSR _DPENADD
@RTS
	RTS
;debate reaction success (attacker)
;_DASUCC
;	LDA #$01
;	LDY #$03
;	STA (OFFSET2),Y
;	RTS
;debate reaction success (defender)
_DDSUCC
	LDA #$01
	LDY #$06
	STA (OFFSET2),Y
	RTS
;debate reaction failure (attacker)
;_DAFAIL
;	LDA #$00
;	LDY #$03
;	STA (OFFSET2),Y
;	RTS
;debate reaction failure (defender)
_DDFAIL
	LDA #$00
	LDY #$06
	STA (OFFSET2),Y
	RTS

;debate_transfer_gain_values()
_DDPADD
	LDX FPARTY
	LDA V_DPCQAG ;passive gain is also "attacker"
	CLC
	ADC V_DDPQ,X
	STA V_DDPQ,X
	JSR _DPACTSUC
	
	LDY #$02
	LDA (OFFSET2),Y
	TAX
	LDA V_DPCQDG ;always defender
	CLC
	ADC V_DDPQ,X
	STA V_DDPQ,X

	RTS
;debate_attacker_add_dp(A = amt)
_DDPADDA
	STA FRET3
	LDX FPARTY
	JSR _DPCHK
	LDA FRET3
	JSR _DDPADDA2
	RTS
;debate_defender_add_dp(A = amt)
_DDPADDD
	STA FRET3
	LDY #$02
	LDA (OFFSET2),Y
	TAX
	JSR _DPCHK
	LDA FRET3
	JSR _DDPADDD2
	RTS
;debate_attacker_add_dp_no_penalty_check(A = amt)
_DDPADDA2
	CLC
	ADC V_DPCQAG
	STA V_DPCQAG
	RTS
;debate_defender_add_dp_no_penalty_check(A = amt)
_DDPADDD2
	CLC
	ADC V_DPCQDG
	STA V_DPCQDG
	RTS
;debate_action_penalty_check(FRET3 = amt)
_DPCHK
	LDA FRET3
	BPL @POS
	JMP _DPENADD
@POS
	RTS
;debate_penalty_add(X = party)
_DPENADD
	JSR _DPENINC
	
	LDA #$01
	STA V_DPCURR,X
	
	LDA #$08
	JSR _RNG
	CMP C_CHAR
	BCC @RTS
	LDA #$01
	STA V_DPOBSC,X
@RTS
	RTS
;debate_penalty_remove(X = party)
_DPENRM
	LDA #00
	STA V_DPPREV,X
	LDA V_DPCOUN,X
	BEQ @RTS
	SEC
	SBC #$01
	STA V_DPCOUN,X
@RTS
	RTS

;debate_penalty_count_increment(X = party)
_DPENINC
	INC V_DPCOUN,X
	RTS
;debate_passive_action_success_check(X = party)
_DPACTSUC
	LDY #$00
	LDA (OFFSET2),Y
	JSR _DAISATK
	BNE @RTS
	
	LDY #$03 ;success (set before branch!)
	LDA V_DPCQAG
	BMI @FAILURE
	
	LDA V_DPCURR,X
	BNE @FAILURE
	
	LDA #$01
	STA (OFFSET2),Y
	RTS
@FAILURE
	LDA #$00
	STA (OFFSET2),Y
@RTS
	RTS
	
;debate_correct_selected_issue(A = issue index)
_DBCS
	CMP V_DISS0
	BEQ @TRUE
	CMP V_DISS1
	BEQ @TRUE
	LDA #00
	RTS
@TRUE
	LDA #01
	RTS
	
;debate_get_log(X = question index (starting with 1), Y = player index)
;sets OFFSET to log block
_DGETLOG
	TYA
	DEX
	STA FY1
	LDY #$1C
	+__LAB2O V_DBLOG
	JSR _OFFSET
	LDX FY1
	LDY #$07
	JSR _OFFSET
	RTS

;debate_add_log(X = question index, starting with 1)
_DADDLOG
	DEX
	+__LAB2O V_DBLOG
	LDY #$1C
	JSR _OFFSET
	LDY #00
@ACTLOOP
	LDA V_DACT,Y
	STA (OFFSET),Y
	INY
	CPY #$1C
	BNE @ACTLOOP
	RTS

;debate_penalty_log_counts(X = question index) 
_DPENLOG
	+__LAB2O V_DPCLOG
	LDY #$04
	JSR _OFFSET
	
	LDY #00
@LOOP
	LDA V_DPCOUN,Y
	STA (OFFSET),Y
	INY
	CPY #$04
	BNE @LOOP
	RTS
	
;debate_question_dp_to_region_cp()
_DAPLYDP
	LDA #00
	STA FPARTY
@PLYLOOP
	LDA FPARTY
	JSR _CANDLOAD
	
	LDX V_DQUEST
	LDA V_DSTATE-1,X
	STA FVAR6 ;question state
	STA FARG1
	JSR _CPOFFS
	
	JSR _DAPLYDP3
	LDA C_HOME
	CMP FVAR6
	BNE @NOMATCH
;if home state, add or subtract additional DP
	LDA V_DDP,X
	BMI @NEG
	INC V_DDP,X
	BNE @POS
@NEG
	DEC V_DDP,X
@POS
@NOMATCH
	JSR _DAPLYDP2

	LDX #$FF
	LDA FVAR6
	JSR _STATEGR
	LDA D_REGLIM-1,X
	STA FVAR5
	LDA D_REGLIM,X
	STA FVAR6 ;region bounds
	
	JSR _DAPLYDP3
@REGLOOP
	LDA FVAR5
	STA FARG1
	JSR _CPOFFS
	JSR _DAPLYDP2
	
	INC FVAR5
	LDA FVAR5
	CMP FVAR6
	BNE @REGLOOP
	
	INC FPARTY
	LDA FPARTY
	CMP S_PLAYER
	BNE @PLYLOOP
	RTS
;debate_add_gain_to_state
_DAPLYDP2
	STY FY1
	LDA (CP_ADDR),Y
	STA FSUS2
	LDA V_DDPQ,X
	JSR _ADDSUS
	LDY FY1
	LDA FSUS1
	STA (CP_ADDR),Y
	RTS
;reset X/Y
_DAPLYDP3
	LDA FPARTY
	TAX
	TAY
	INY
	RTS

;debate_add_dp_gain_to_dp_total()
_DDPTOTL
	LDX #00
@LOOP
	LDA V_DDP,X
	CLC
	ADC V_DDPQ,X
	BMI @NEG
	CMP #$25
	BCC @NOVF
	LDA #$24
	BNE @NOVF
@NEG
	CMP #$DC
	BCS @NOVF
	LDA #$DC
@NOVF
	STA V_DDP,X
	LDA #00
	STA V_DDPQ,X
	INX
	CPX S_PLAYER
	BNE @LOOP
	RTS

;debate_audience_reaction_draw()
_DDRWAUD
	LDA #00
	STA FVAR5
	
	LDX #$0A
	STX FX1 ;store row
@TOP
	LDA FX1
	STA GX_CROW
	LDA #P_DAUDC
	STA GX_CCOL
	
	LDA FVAR5
	BEQ @SKIP
	LDY #$02
	LDA (OFFSET2),Y
	TAY
	LDA #$01
	BNE @SKIP2
@SKIP
	LDY FPARTY
@SKIP2
	STY FVAR6 ;target
	LDA V_DPOBSC,Y
	BNE @DRAW ;A = 1, boos
	
	LDA V_DPCURR,Y
	BNE @MURMUR
	
	LDA #$04
	JSR _RNG
	BEQ @DRAW
@MURMUR
	LDA #$02 ;murmurs
@DRAW
	JSR _DDRWAU2
	
	LDX FX1
	INX
	STX FX1
	STX GX_CROW
	LDA #P_DAUDC
	STA GX_CCOL
	
	LDA FVAR6
	STA V_PARTY
	JSR _DRWPLYR
	
	LDY #$04
	LDA (OFFSET2),Y
	CMP #$FF
	BEQ @RTS ;exit if no reaction
	
	INC FVAR5
	LDX FX1
	INX
	INX
	STX FX1
	LDA FVAR5
	CMP #$02
	BNE @TOP
@RTS
	RTS
;debate_draw_audience_text(A = reaction)
_DDRWAU2
	STA FARG1
	+__LAB2XY T_DAUDI
	JSR _GX_STR
	LDX FARG1
	+__LAB2O T_DAUDI
	LDA OFFSET
	CLC
	ADC D_DBAUDI,X
	STA OFFSET
	BCC @CARRY
	INC OFFSET+1
@CARRY
	+__O2XY
	JSR _GX_STR
	RTS
	
;divide_by_2_signed(A = value)
_DIV2S	
	BPL @LSR
	SEC
	ROR
	RTS
@LSR
	LSR
	RTS
	
;debate_draw_log()
_DDRWLOG
	JSR _GX_CLRS
	JSR _DRWBORD2
	
	+__COORD P_DLOGOR,P_DLOGOC
	+__LAB2XY T_DLOG
	JSR _GX_STR
	
	+__COORD P_DLOGPR,P_DLOGPC
	LDA FPARTY
	STA V_PARTY
	JSR _DRWPLYR
	
	+__COORD P_DLOGNR,P_DLOGNC
	JSR _DDRWTV
	
	LDA #00
	STA FSTATE ;question index
@TOPLOOP
	LDA FSTATE
	CLC
	ADC #P_DLOGTR
	STA GX_CROW
	LDA #P_DLOGTC
	STA GX_CCOL

	LDA #C_YELLOW
	STA GX_DCOL
	
	LDX FSTATE
	LDA V_DSTATE,X
	JSR _DRWPOST
	
	INC GX_CCOL
	
	JSR _DRWSPAC
	
	LDX FSTATE
	INX
	STX V_DQUEST
	JSR _DRWTOPC
	INC FSTATE
	LDA FSTATE
	CMP #$03
	BNE @TOPLOOP

	LDA #$01
	STA FSTATE
	LDA #P_DLOGAR
	STA FVAR6 ;store row
@ACTLOOP
	LDA FVAR6
	STA GX_CROW
	LDA #P_DLOGTC
	STA GX_CCOL
	
	LDA #C_PINK
	STA GX_DCOL
	
	+__LAB2O V_DPCLOG
	LDX FSTATE
	DEX
	LDY #$04
	JSR _OFFSET
	LDY FPARTY
	LDA (OFFSET),Y
	TAX
	LDA #00
	JSR _INT
	+__LAB2XY V_STRING
	JSR _GX_STR
	
	LDX FSTATE
	LDY FPARTY
	JSR _DGETLOG
	
	LDA OFFSET
	STA OFFSET2
	LDA OFFSET+1
	STA OFFSET2+1
	
	LDA FVAR6
	STA GX_CROW
	LDA #P_DLOGA2
	STA GX_CCOL
	
	LDY #00
	LDA (OFFSET2),Y
	JSR _DRWACOD
	
	JSR _DRWSPAC
	
	LDY #$01
	LDA (OFFSET2),Y
	CMP #$FF
	BEQ @ISSUE
	JSR _DDRWISS
@ISSUE
	
	LDA FVAR6
	STA GX_CROW
	LDA #P_DLOGA3
	STA GX_CCOL
	
	LDY #$03
	LDA (OFFSET2),Y
	JSR _DRWSIGN
	
	JSR _DRWSPAC
	
	LDY #$02
	LDA (OFFSET2),Y
	STA V_PARTY
	CMP #$FF
	BEQ @OPPONT
	JSR _DRWPLYR
@OPPONT
	
	INC FVAR6
	LDA FVAR6
	STA GX_CROW
	LDA #P_DLOGA4
	STA GX_CCOL
	
	LDY #$04
	LDA (OFFSET2),Y
	CMP #$FF
	BEQ @LOOPDONE
	
	LDX V_PARTY
	JSR _DRWPN1
	
	JSR _DRWSPAC
	
	LDY #$04
	LDA (OFFSET2),Y
	JSR _DRWRCOD
	
	JSR _DRWSPAC
	
	LDY #$05
	LDA (OFFSET2),Y
	CMP #$FF
	BEQ @RISSUE
	JSR _DDRWISS
@RISSUE
	LDA FVAR6
	STA GX_CROW
	LDA #P_DLOGA5
	STA GX_CCOL
	
	LDY #$06
	LDA (OFFSET2),Y
	JSR _DRWSIGN
@LOOPDONE
	
	INC FVAR6
	INC FSTATE
	LDA FSTATE
	CMP #$04
	BEQ @DONE
	JMP @ACTLOOP
@DONE
	JSR _FTC
	RTS

;debate_draw_action_code(A = action index)
_DRWACOD
	TAX
	LDA D_DACODE,X
	TAX
	STX FX1
	
	LDA #C_WHITE
	STA GX_DCOL
	
	LDA T_DAMENU,X
	STA GX_CIND
	JSR _GX_CHAR
	INC GX_CCOL
	LDX FX1
	LDA T_DAMENU+1,X
	STA GX_CIND
	JSR _GX_CHAR
	INC GX_CCOL
	RTS
	
;debate_draw_reaction_code(A = reaction index)
_DRWRCOD
	TAX
	LDA D_DRCODE,X
	TAX
	STX FX1
	
	LDA #C_WHITE
	STA GX_DCOL
	
	LDA T_DRMENU,X
	STA GX_CIND
	JSR _GX_CHAR
	INC GX_CCOL
	LDX FX1
	LDA T_DRMENU+1,X
	STA GX_CIND
	JSR _GX_CHAR
	INC GX_CCOL
	RTS
	
;draw_space()
;draws a single space (draw color unchanged)
_DRWSPAC
	LDA #VK_SPACE
	STA GX_CIND
	JSR _GX_CHAR
	INC GX_CCOL
	RTS
	
;debate_clear_menu_right() 
;clears more than CLRMENR
_DCLRMR 
	LDA #P_DMRX1
	STA GX_LX1
	LDA #P_DMRX2


	STA GX_LX2
	LDA #P_DMRY1
	STA GX_LY1
	LDA #P_DMRY2
	STA GX_LY2
	JSR _GXRECTC
	RTS 

;debate_ai_action_select()
_DACTAI
	;JSR _DDEBUGA
	;RTS
	LDX FPARTY
	LDA V_AI,X
	CMP #$02
	BCS @NOTEASY
	
	LDY #00
	LDA #DBPIVOT
	STA (OFFSET2),Y
	LDY #01
	LDA #$06
	JSR _RNG
	STA (OFFSET2),Y
	RTS
@NOTEASY

	LDA #$02
	JSR _RNG
	BEQ @MORALIZE
	
	LDX #00
@CHECKPEN
	LDA V_DPOBSC,X
	BEQ @SEEN
	CPX FPARTY
	BEQ @SEEN
	
	TXA
	LDY #02
	STA (OFFSET2),Y
	LDA #DBPERSNL
	JMP @DONE
@SEEN
	INX
	CPX S_PLAYER
	BNE @CHECKPEN
@MORALIZE
	
	JSR _DAIGCS
	STX FX1
	LDY FPARTY
	JSR _DBICAI
	BEQ @DIFFER
	
	LDX FX1
	JSR _DAIGOPP
	TAY
	STY FY1
	JSR _DBICAI
	BNE @CHALLENGE
	
	LDA FX1
	LDY #01
	STA (OFFSET2),Y
	LDA FY1
	LDY #$02
	STA (OFFSET2),Y
	LDA #DBMORAL
	JMP @DONE
@DIFFER
	JSR _DAIGCS
	STX FX1
	JSR _DAIGOPP
	TAY
	STY FY1
	JSR _DBICAI
	BNE @CHALLENGE
	
	LDA FX1
	LDY #01
	STA (OFFSET2),Y
	LDA FY1
	LDY #$02
	STA (OFFSET2),Y
	LDA #DBDIFFER
	JMP @DONE
@CHALLENGE
	JSR _DAIGCS
	STX FX1
	LDY FPARTY
	JSR _DBICAI
	BNE @ANSWER
	
	LDA FX1
	LDY #01
	STA (OFFSET2),Y
	LDA #DBCHALL
	JMP @DONE
@ANSWER
	JSR _DAIGCS
	LDY FPARTY
	JSR _DBICAI
	BEQ @PIVOT
	
	LDA FX1
	LDY #01
	STA (OFFSET2),Y
	LDA #DBANSWER
	JMP @DONE
@PIVOT
	JSR _DAIGNS
	TXA
	LDY #01
	STA (OFFSET2),Y
	LDA #DBPIVOT
	JMP @DONE
@DONE
	LDY #00
	STA (OFFSET2),Y
	RTS

;debate_ai_reaction_select()	
_DRCTAI
	;RTS
	LDX FPARTY
	LDA V_AI,X
	CMP #$02
	BCS @NOTEASY

	LDY #04
	LDA #$00
	STA (OFFSET2),Y
@NOTEASY
	LDY #04
	LDA #$00
	STA (OFFSET2),Y
@RTS
	RTS
	
;debate_sort_candidates_by_ec()
_DECSORT
	
	JSR _SUMEC
	
	LDX #00
	LDY #00
@DIVLOOP
	LSR V_SUMEC+0,X
	LSR V_SUMEC+0,X
	LDA V_SUMEC+1
	BEQ @SKIP
	LDA V_SUMEC+0,X
	CLC
	ADC #$80
	STA V_SUMEC+0,X
@SKIP
	INX
	INX
	INY
	CPY S_PLAYER
	BNE @DIVLOOP
	
	LDA #00
	TAX
@CLRLOOP
	STA V_PRIOR,X
	STA V_PRIOR2,X
	INX
	CPX #29
	BNE @CLRLOOP
	
	LDX #00
	LDY #00
@PLYLOOP
	LDA V_SUMEC,X
	CLC
	ADC #$01
	STA V_PRIOR,Y
	TYA
	STA V_PRIOR2,Y
	INX
	INX
	INY
	CPY S_PLAYER
	BNE @PLYLOOP

	JSR _AISORT

	RTS

;debate_ai_get_correct_issue()
;X = issue index
_DAIGCS
	LDA #$02
	JSR _RNG
	BEQ @ONE
	LDX V_DISS0
	RTS
@ONE
	LDX V_DISS1
	RTS
	
;debate_ai_get_incorrect_issue()
;X = issue index
_DAIGNS
	LDA #$06
	JSR _RNG
	CMP V_DISS0
	BEQ _DAIGNS
	CMP V_DISS1
	BEQ _DAIGNS
	TAX
	RTS
	
;debate_ai_get_opponent()
;returns A = highest non-self player index
_DAIGOPP
	LDX #00
@PLYLOOP
	LDA V_PRIOR2,X
	STA FVAR6
	CMP FPARTY
	BEQ @SKIP
	
	LDA #$03 ;1/3 chance to skip to next opponent in list
	JSR _RNG
	BEQ @SKIP
	
	LDA FVAR6
	RTS
@SKIP
	INX
	CPX S_PLAYER
	BNE @PLYLOOP
	JMP _DAIGOPP

;candidate_others_values()
;calculates "OTHER" values for all candidates
_CANDOTHR
	LDA #$00
	STA V_PARTY
@LOOP
	LDA V_PARTY
	JSR _CANDLOAD
	
	LDX #00
@ISSLOOP
	LDA C_ISSUES,X
	STA V_STRING,X
	INX
	CPX #$05
	BNE @ISSLOOP
	
	JSR _AVGISSU
	LDA FRET1
	LDX V_PARTY
	STA V_DOTHER,X
	INC V_PARTY
	INX
	CPX S_PLAYER
	BNE @LOOP
	
	LDA #00
	JSR _CANDLOAD
	RTS
	
;averages issue values
;FRET1 = VALUE
_AVGISSU 
	LDA #$00
	TAX 
@ADDLOOP 
	CLC 
	ADC V_STRING,X
	INX 
	CPX #$05
	BNE @ADDLOOP
	TAY 
	LDA #00
	JSR _162FAC
	JSR _FAC2ARG
	LDA #$00
	LDY #$05
	JSR _162FAC
	JSR _DIVIDE	
	JSR _FAC2STR
	LDA V_STRING+2
	BNE @SKIP
	LDA V_STRING+1
	BNE @SKIP2
@SKIP 
	LDA V_STRING+3
	CMP #$35
	BCC @CARRY
	INC V_STRING+1
@CARRY 
	LDA V_STRING+1
@SKIP2 
	SEC 
	SBC #$30
	STA FRET1
	RTS 

_DINIT 
	LDA #00
	TAX 
@LOOP 
	STA V_DPCOUN,X
	STA V_DPOBSC,X
	STA V_DPCURR,X
	STA V_DPPREV,X
	STA V_DDP,X
	STA V_DDPQ,X
	STA V_DNATL,X
	INX 
	CPX #$04
	BNE @LOOP
	RTS 



