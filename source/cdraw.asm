;cdraw.asm
;CAMP03 
;most draw routines

;draw_popular_ec() 
;LOCAL: FVAR2
;_POPSUM called beforehand
;draws total popular vote and EC counts on left
_DRWPOP
	JSR _SUMEC2
_DRWPOP2

	LDA #P_TOP
	STA GX_CROW

	LDA #00
	STA FVAR2
@LOOP 
	LDA #P_LEFT
	STA GX_CCOL

	LDX FVAR2
	JSR _DRWPN3
	INC GX_CROW

	LDA #P_LEFT
	STA GX_CCOL

	LDY FVAR2
	TYA 
	ASL 
	TAY 
	LDA V_SUMEC,Y
	TAX 
	LDA V_SUMEC+1,Y
	JSR _INTCMB2

	INC GX_CROW

	LDA #P_LEFT
	STA GX_CCOL
	
	LDA FVAR2
	CMP #UND_PRTY
	BEQ @SKIPUND
	STA FARG1
	
	LDX #00
	JSR _DRWPOPV
	INC GX_CROW
@SKIPUND 
	INC FVAR2
	LDA FVAR2	
	CMP #UND_PRTY+1
	BEQ @MOE
	CMP S_PLAYER
	BNE @LOOP
	
	LDA #UND_PRTY
	STA FVAR2
	JMP @LOOP
	
@MOE
	LDA V_GAMEOV
	BNE @RTS
	
	LDA #C_WHITE
	STA GX_DCOL
	LDA #P_LEFT
	STA GX_CCOL
	+__LAB2XY T_MOE
	JSR _GX_STR
	LDA #P_LEFT
	STA GX_CCOL
	LDX V_MOE
	JSR _INTCMB2
	LDA #VK_PERC
	DEC GX_CCOL
	STA GX_CIND
	JSR _GX_CHAR
@RTS
	RTS 

;draw_popular_vote(FARG1 = party, X = [long format = true])
_DRWPOPV
	STX FX1
	+__LAB2O V_POPVOTE
	LDX FARG1
	LDY #PERCLEN
	JSR _OFFSET
	
	LDX FARG1
	LDA V_PTCOL,X
	STA GX_DCOL
	
	LDY #00
@COPY
	LDA (OFFSET),Y
	STA V_FPOINT,Y
	INY
	CPY #PERCLEN
	BNE @COPY
	
	LDA FX1
	BNE @SKIP
	JSR _PERCTRUNC2
	JMP @DRW
@SKIP
	JSR _FSTRAP
@DRW
	+__LAB2XY V_FPOINT
	JSR _GX_STR
	RTS

;draw_staff() 
;draws whether the staff is available or not
_DRWSTAF
	LDA S_SKIPGAME
	BEQ @SKIPGAME
	RTS
@SKIPGAME

	+__COORD P_STAFFR,P_STAFFC
	+__LAB2XY T_STAFF
	JSR _GX_STR

	LDA V_STAFFOUT
	BEQ @OK
	+__LAB2XY T_STAFFO
	JSR _GX_STR
	RTS 
@OK 
	+__LAB2XY T_STAFFG
	JSR _GX_STR
	RTS 

;draw_party_name_3char(X=party index)
;row,col loaded beforehand
_DRWPN3 
	LDA V_PTCOL,X
	STA GX_DCOL
	+__LAB2O V_PTCHR3
	LDY #$04
	JSR _OFFSET
	+__O2XY
	JSR _GX_STR
	RTS 

;draw_party_char(X=party index)
_DRWPN1 
	LDA V_PTCOL,X
	STA GX_DCOL
	LDA V_PTCHAR,X


	STA GX_CIND
	JSR _GX_CHAR
	INC GX_CCOL
	RTS 

;draw_winning_party() 
;region in C_CREG
_DRWWIN 
	LDA #P_WINPTR
	STA GX_CROW

	LDX C_CREG
	LDA D_REGLIM,X
	STA FVAR3
	LDA D_REGLIM-1,X
	STA FSTATE
@LOOP 
	LDA #P_WINPTC
	STA GX_CCOL
	JSR _DRWSPAC

	LDX FSTATE
	LDA V_CTRL,X
	TAX 
	JSR _DRWPN1
	LDX FSTATE
	LDA V_TIERES,X
	BEQ @SKIPTIE
	ORA #$30
	STA GX_CIND
	JSR _GX_CHAR
	JMP @SKIPSPC
@SKIPTIE 
	LDA #$20
	STA GX_CIND
	JSR _GX_CHAR
@SKIPSPC 
	INC GX_CROW

	INC FSTATE
	LDA FSTATE
	CMP FVAR3
	BNE @LOOP
	RTS 

;clear_bottom_left() 
;clears bottom left
_CLRBL 
	LDA #P_LEFT
	STA GX_LX1
	LDA #P_POSTLR
	STA GX_LY1
	LDA #P_BL2
	STA GX_LX2
	LDA #P_BOTTOM
	STA GX_LY2
	JSR _GXRECTC
	RTS 

;clear_bottom_left_status() 
;clears candidate info only
_CLRBL2 
	LDA #P_LEFT
	STA GX_LX1
	LDA #P_POSTLR
	STA GX_LY1
	LDA #P_BL2
	STA GX_LX2
	LDA #P_POSTLR2
	STA GX_LY2
	JSR _GXRECTC
	RTS 

;draw_candidate() 
;primary,secondary,issues,title,name,home state
;health, funds, cumulative bonus
_DRWCAND 
	LDA S_SKIPGAME
	BEQ @SKIPGAME
	RTS
@SKIPGAME
	JSR _CLRBL2
	LDA V_REDRW1
	BNE @SKIPDRW
	+__LAB2XY T_MENUBL
	+__COORD P_MENBLR,P_LEFT
	JSR _GX_STR
	INC V_REDRW1 ;MUST BE RESET TO DRAW!
@SKIPDRW 
	
	JSR _DRWPRIM
	JSR _DRWSECN
	JSR _DRWISSU
	JSR _DRWOTHR

	LDA #00
	STA GX_LX1
	LDA #$1D
	STA GX_LX2
	LDA #P_POSTLR
	STA GX_LY1
	LDA #P_MONEYR
	STA GX_LY2
	JSR _GXRECTC

	LDX V_PARTY
	LDA V_PTCOL,X
	STA GX_DCOL
	+__COORD P_POSTLR,P_POSTLC
	LDA C_HOME
	JSR _DRWPOST

	LDA #P_PTTLC
	STA GX_CCOL
	JSR _DRWPTTL
	LDA V_PARTY
	JSR _DRWPLYR
	JSR _DRWAI
	JSR _DRWCPRO
	JSR _DRWVP

	LDA V_STAFFOUT
	BEQ @SKIPJMP
	JMP @SKIPHL
@SKIPJMP 

	JSR _DRWFUND

	LDA #P_HEALC
	STA GX_CCOL
	LDY #00
	LDA C_HEALTH
	LSR 
	LSR 
	LSR 
	LSR 
	LSR ;/32
	STA FVAR1
	LDA C_HEALTH
	BEQ @SKIPINC
	INC FVAR1 ;only if non-zero
@SKIPINC 
	LDA FVAR1
	CMP #$02
	BCC @DANGER
	CMP #$04
	BCS @OKHEAL
	LDA #C_YELLOW ;warning
	BNE @COLOR
@DANGER 
	LDA #C_PINK
	BNE @COLOR
@OKHEAL 
	LDA #C_LGREEN
@COLOR 
	STA GX_DCOL
	STA FVAR2
	+__LAB2XY T_HEALTH
	JSR _GX_STR

	LDA #P_HEAL2C
	STA GX_CCOL
	LDA FVAR1
	BEQ @SKIPHL
	LDA FVAR2
	STA GX_DCOL
@DLOOP 
	LDA #ASTERISK
	STA GX_CIND
	JSR _GX_CHAR
	INC GX_CCOL
	DEC FVAR1
	LDA FVAR1
	BNE @DLOOP
@SKIPHL 
	JSR _DRWVBON
	RTS 

;draw_other()
;draws "OTHER" issue value
_DRWOTHR
	LDA V_DEBON
	BEQ @SKIP
	+__COORD P_ISSUER-1,P_ISSUEC-6
	+__LAB2XY T_OTHER
	JSR _GX_STR
	LDA #C_LBLUE
	STA GX_DCOL
	LDX V_PARTY
	LDA V_DOTHER,X
	ORA #NUMBERS
	STA GX_CIND
	JSR _GX_CHAR
@SKIP
	RTS
	
;draw_vp()
;draws VP's home state
_DRWVP
	+__COORD P_VPSTAR,P_VPSTAC
	+__LAB2XY T_VPSTA
	JSR _GX_STR
	LDA C_VP
	JSR _DRWPOST
	RTS

;draw_candidate_profile()
;candidate is xabcdeee, where abc is L-shoulder,body,R-shoulder, d is long hair, eee is hair, x is blank
_DRWCPRO
	+__COORD P_CPRBOXR,P_CPRBOXC
	+__LAB2XY T_PROFILE
	JSR _GX_STR
	
	+__COORD P_CPROFR,P_CPROFC
	
	LDX V_PARTY
	LDA V_PTCOL,X
	STA GX_DCOL
	
	LDA V_PROFILE,X
	STA FVAR1
	AND #%00000111
	TAX
	LDA D_HAIR,X
	
	STA GX_CIND
	JSR _GX_CHAR ;"HAIR" - 3 BITS
	
	INC GX_CROW
	LDA FVAR1
	AND #%00001000
	BEQ @SKIPLONG
	
	DEC GX_CCOL ;LONG HAIR - 1 BIT
	LDA #$46 + ALPHAS
	JSR _GX_STR2
	INC GX_CCOL
	LDA #$47 + ALPHAS
	STA GX_CIND
	JSR _GX_CHAR
	DEC GX_CCOL
@SKIPLONG
	LDA V_WEEK
	BEQ @OK

	LDA V_STAFFOUT
	BEQ @STAFF
	LDA #$43 + ALPHAS
	BNE @DONE
@STAFF
	JSR _MAXEC
	SEC
	SBC #$01
	STA FVAR2	

	LDA C_HEALTH
	CMP #32
	BCS @NOTDEAD
@SAD
	LDA FVAR2
	CMP V_PARTY
	BEQ @NEUTRAL
	LDA #$42 + ALPHAS
	BNE @DONE
@NOTDEAD
	LDA C_MONEY
	CMP #32
	BCS @NOTPOOR
	BCC @SAD
@NOTPOOR
	LDA FVAR2
	CMP V_PARTY
	BEQ @OK
@NEUTRAL
	LDA #$41 + ALPHAS
	BNE @DONE
@OK
	LDA #$40 + ALPHAS
@DONE
	STA GX_CIND
	JSR _GX_CHAR ;HEAD - NO BITS
	
	INC GX_CROW
	DEC GX_CCOL
	
	LDA FVAR1
	AND #%01000000
	BEQ @SKIP
	LDA #$03
@SKIP
	CLC
	ADC #$4D + ALPHAS
	JSR _GX_STR2 ;L SHOULDER - 1 BIT
	
	LDA FVAR1
	AND #%00100000
	BEQ @SKIP2
	LDA #$03
@SKIP2
	CLC
	ADC #$4E + ALPHAS
	JSR _GX_STR2 ;BODY - 1 BIT
	LDA FVAR1
	AND #%00100000
	BEQ @SKIP3
	LDA #$03
@SKIP3
	CLC
	ADC #$4F + ALPHAS
	JSR _GX_STR2 ;R SHOULDER - 1 BIT
	
	RTS

;draw_visit_bonus() 
_DRWVBON 
	+__COORD P_VBONSR,P_VBONSC

	LDA V_DEBON
	BNE @RTS
	
	LDA V_STAFFOUT
	BEQ @SKIPO
	LDX #00
@CLRLOOP 
	LDA #16
	JSR _DRWBLANK
	RTS 
@SKIPO 

	+__LAB2XY T_BONUS
	JSR _GX_STR

	LDA #P_VBON2C
	STA GX_CCOL
	LDA V_VBONUS
	AND #$80
	BNE @MINUS
	LDA #PLUS
	STA GX_CIND
	LDA #C_LGREEN
	STA GX_DCOL
	BNE @DRAW
@MINUS 
	LDA #MINUS
	STA GX_CIND
	LDA #C_PINK
	STA GX_DCOL
@DRAW 
	JSR _GX_CHAR
	INC GX_CCOL
	LDA V_VBONUS
	AND #$7F
	JSR _DRWPOST
@RTS
	RTS 

;draw_player_name(A=PLAYER INDEX)
;gx_ccol,gx_crow set beforehand
_DRWPLYR 
	TAX 
	LDA V_PTCOL,X
	STA GX_DCOL
	+__LAB2O V_PNAME
	LDY #NAMELEN
	JSR _OFFSET
	LDX OFFSET
	LDY OFFSET+1
	JSR _GX_STR
	JSR _DRWSPAC
	RTS 

;draw_postal_code(A=STATE INDEX)
;gx_ccol,gx_crow,gx_dcol set beforehand
;LOCAL: FX1
_DRWPOST
	ASL
	TAX 
	DEX 
	DEX 
	LDA T_POSTAL,X
	STX FX1
	JSR _GX_STR2
	LDX FX1
	INX 
	LDA T_POSTAL,X
	JSR _GX_STR2
	RTS
;extra space
_DRWPOST2
	JSR _DRWPOST
	INC GX_CCOL
	RTS

;draw_player_title() 
;gx_dcol, gx_ccol, gx_crow set beforehand
_DRWPTTL 
	+__LAB2XY D_CANDID
	LDA C_INCUMB
	BEQ @NOTINC
	TXA 
	CLC 
	ADC #$3C
	BCC @NC
	INY 
@NC 
	TAX 
	JMP @DRAW
@NOTINC 
	TXA 
	CLC 
	ADC C_TITLE
	BCC @NOCARRY
	INY 
@NOCARRY 
	TAX 
@DRAW 
	JSR _GX_STR
	RTS 

;draw_issue_values() 
;current candidate
_DRWISSU 
	LDA #C_LBLUE
	STA GX_DCOL
	+__COORD P_ISSUER,P_ISSUEC

	LDX #00
@LOOP 
	LDA C_ISSUES,X
	CMP #ISSUEX
	BEQ @ISSUEX
	CMP #ISSUEN
	BEQ @ISSUEN
	JMP @CHAR
@ISSUEX
	LDA #ASTERISK
	BNE @CHAR2
@ISSUEN
	LDA #MINUS
	BNE @CHAR2
@CHAR
	ORA #$30
@CHAR2
	STA GX_CIND
	STX FVAR1

	JSR _GX_CHAR
	LDX FVAR1
	INX 
	INC GX_CROW
	CPX #ISSUEC
	BNE @LOOP
	RTS 

;draw_primary stats()
;current candidate
_DRWPRIM 
	LDA #C_VIOLET
	STA GX_DCOL
	LDA #P_PRIMR
	STA GX_CROW
	LDX #00

@LOOP 
	LDA #P_PRIMC
	STA GX_CCOL
	LDA C_CHAR,X
	ORA #$30
	STA GX_CIND
	STX FVAR1
	JSR _GX_CHAR
	LDX FVAR1
	INX 
	INC GX_CROW
	CPX #$05
	BNE @LOOP
	RTS 

;draw_secondary_stats() 
;current candidate
_DRWSECN 
	LDA #C_VIOLET
	STA GX_DCOL
	LDA #P_SECNDR
	STA GX_CROW

	LDY #00
@LOOP 
	LDA #C_VIOLET
	STA GX_DCOL
	LDA #P_SECNDC
	STA GX_CCOL
	LDA C_CER,Y
	TAX 
	LDA #00
	STY FVAR1
	JSR _INTCMB2

	INC GX_CROW
	LDY FVAR1
	INY 
	CPY #$05
	BNE @LOOP
	RTS 
	
;draw_region_states(A=region) 
;draws upper right state list for given region
;LOCAL: FVAR2,FVAR3
_DRWREGS 
	;draw region at top
	STA FSTATE
	DEC FSTATE
	TAX 
	+__COORD P_REGTPR,P_REGTPC
	LDA #C_LGRAY
	STA GX_DCOL

	JSR _DRWREGNI
	;get region bounds
	LDX FSTATE
	LDA D_REGLIM,X
	STA FVAR2
	LDA D_REGLIM+1,X
	STA FVAR3

	LDA #P_REGLSR
	STA GX_CROW
@LOOP 
	LDA #C_LBLUE
	STA GX_DCOL
	LDA #P_REGLSC
	STA GX_CCOL

	LDX FVAR2
	LDA #00
	JSR _INTCMB
	
	LDA #P_REGLSC2
	STA GX_CCOL
	
	LDX FVAR2
	JSR _LDACTRL
	TAX
	LDA V_PTCOL,X
	STA GX_DCOL
	LDA FVAR2
	JSR _DRWPOST2 ;postal code

	LDX FVAR2
	JSR _DRWSTEC ;ec

	INC GX_CROW
	INC FVAR2
	LDA FVAR2
	CMP FVAR3
	BNE @LOOP
	;end of game draw control party + tie
	RTS 

;draw_state_ec(X=state index)
;draws state's electoral college votes
;gx_dcol,gx_row,gx_col set before call
_DRWSTEC
	LDA V_EC,X
	TAX 
	LDA #00
	JSR _INTCMB2
	RTS 
;draw_region_menu() 
;draws region selection menu
_DRWREGM 
	LDA #01
	STA GX_REG
	+__COORD P_REGLSR2,P_REGLSC
@LOOP
	LDA #P_REGLSC
	STA GX_CCOL
	LDX GX_REG
	JSR _DRWREGNI
	INC GX_CROW
	INC GX_REG
	LDA GX_REG
	CMP #REGION_C+1
	BNE @LOOP
	
	LDA #P_REGLSC
	STA GX_CCOL
	+__LAB2XY T_TRAVEL
	JSR _GX_STR
	RTS 

;draw_region_name(x=region index starting at 1)
_DRWREGN
	+__LAB2O T_REGION
	DEX 
	LDY #09
	JSR _OFFSET
	+__O2XY
	JSR _GX_STR
	RTS
	
;draw_region_name_with_index(x=region index starting at 1)
_DRWREGNI
	STX FX1
	LDA #C_LBLUE
	STA GX_DCOL
	TXA
	ORA #NUMBERS
	JSR _GX_STR2
	JSR _DRWSPAC
	LDA #C_WHITE
	STA GX_DCOL
	LDX FX1
	JSR _DRWREGN
	RTS

;clear_menu_right() 
;clears the campaign selection menu
_CLRMENR 
	LDA #P_RSELC
	STA GX_LX1
	LDA #P_MENUR2
	STA GX_LY1
	LDA #P_RIGHT
	STA GX_LX2
	LDA #$0F
	STA GX_LY2
	JSR _GXRECTC
	RTS 

;draw_menu_right() 
;draws the campaign selection menu
_DRWMENR 
	LDA S_SKIPGAME
	BEQ @SKIPGAME
	RTS
@SKIPGAME
	
	LDA V_REDRW2

	BNE @NODRAW
	LDA #01
	STA V_REDRW2
	RTS 
@NODRAW 
	JSR _CLRMENR

	LDX V_PARTY
	LDA V_PTCOL,X
	STA GX_DCOL
	LDA #C_WHITE
	STA GX_BCOL
	+__LAB2XY T_BANNER
	+__COORD P_MENURR,P_MENURC
	JSR _GX_STR
	JSR _DRWWEEK

	LDX C_CREG
	+__COORD P_MENUR2,P_MENURC
	JSR _DRWREGNI

	+__LAB2XY T_MENUR
	+__COORD P_MENUR3,P_MENURC
	JSR _GX_STR
	
	RTS 

;draw_week() 
;draws the current week
_DRWWEEK 
	+__COORD P_WEEKR,P_WEEKC
	LDA #10
	JSR _DRWBLANK
	+__COORD P_WEEKR,P_WEEKC+1
	+__LAB2XY T_WEEK
	JSR _GX_STR
	
	LDX V_WEEK
	LDA #00
	JSR _INTCMB
	RTS 

;draw_title_2() 
;the american banner and title text
_DRW_T2 
	+__COORD P_TITL2R,P_TITL2C
	+__LAB2XY T_TITLE2
	JSR _GX_STRA

	+__COORD P_TITL2R2,P_LEFT
	+__LAB2XY T_T2
	JSR _GX_STR
	RTS 

;draw_clear_title2()
_CLR_T2 
	LDA #P_LEFT
	STA GX_LX1
	LDA #P_RIGHT
	STA GX_LX2
	LDA #P_TITL2R2
	STA GX_LY1
	LDA #P_TITL2R3
	STA GX_LY2
	JSR _GXRECTC
	RTS 

;incumbent_menu() 
_DRWINCM 
	JSR _CLRBR
	+__COORD P_INCUMR,P_INCUMC
	+__LAB2XY T_INCUMB
	JSR _GX_STR

	LDX #P_INCUMENUR
	LDY #P_INCUMENUR+INCUMBENT_C
	JSR _RSELECT
	BEQ @RTS
	CMP #INCUMBENT_C
	BEQ @FRINGE
	ORA #$F0
	STA C_INCUMB
@RTS 
	JSR _CLRBR
	RTS
@FRINGE
	LDX V_PARTY
	LDA #01
	STA V_FRINGE,X
	RTS
	
;draw_ai_menu 
_DRWAIM 
	JSR _CLRBR
	+__COORD P_AIMENR,P_AIMENC
	+__LAB2XY T_AI
	JSR _GX_STR

	LDX #P_AIMENR+1
	LDY #P_AIMENR+1+AI_MAX
	JSR _RSELECT
	LDX V_PARTY
	STA V_AI,X

	JSR _CLRBR
	RTS 

;draw_week_schedule() 
_DRWWSCH 
	LDA S_SKIPGAME
	BEQ @SKIPGAME
	RTS
@SKIPGAME
	
	JSR _CLRBR
	LDX V_PARTY
	LDA V_PTCOL,X
	STA GX_DCOL
	+__COORD P_SCHEDR,P_SCHEDC
	+__LAB2XY T_SCHED
	JSR _GX_STR

	RTS 

;draw_popular_vote_final() 
;draws the popular vote list and the winner's conditions
;LOCAL: FVAR1
_DRWPOPF
	+__COORD P_ENDPOR,P_ENDPOC

	LDA #C_WHITE
	STA GX_DCOL
	+__LAB2XY T_WINPOP
	JSR _GX_STR

	LDA #P_ENDP2R
	STA GX_CROW
	LDA #00
	STA FVAR1
@LOOP 
	LDA #P_ENDP2C
	STA GX_CCOL

	LDX FVAR1
	JSR _DRWPN3
	INC GX_CCOL
	LDA FVAR1
	STA FARG1
	LDX #01
	JSR _DRWPOPV

	INC GX_CROW
	INC FVAR1
	LDA FVAR1
	CMP S_PLAYER
	BNE @LOOP

	INC GX_CROW

	LDA #P_ENDP22
	STA GX_CCOL

	JSR _CALCWIN
	STA FRET2
	DEC FRET1
	LDA FRET1 ;winning party
	STA V_WINNER
	JSR _DRWPLYR

	INC GX_CCOL

	LDA #C_WHITE
	STA GX_DCOL
	+__LAB2XY T_WINBY
	JSR _GX_STR

	INC GX_CROW

	LDA #P_WINC
	STA GX_CCOL
	LDA #C_YELLOW
	STA GX_DCOL

	+__LAB2O T_WIN
	LDX FRET2 ;winning criteria
	LDY #$10
	JSR _OFFSET
	+__O2XY
	JSR _GX_STR

	RTS 

;draw_funds() 
_DRWFUND 
	+__COORD P_MONEYR,P_MONEYC
	+__LAB2XY T_MONEY
	JSR _GX_STR
	LDA #00
	LDX C_MONEY
	JSR _INTCMB2
	RTS 

;draw_ai_level()
_DRWAI
	+__COORD P_AILVLR,P_AILVLC
	LDX V_PARTY
	LDA V_AI,X
	BEQ @CLR
	+__LAB2XY T_AILEVEL
	JSR _GX_STR
	LDX V_PARTY
	LDA V_AI,X
	ORA #NUMBERS
	STA GX_CIND
	JSR _GX_CHAR
	RTS
@CLR
	LDA #$05
	JSR _DRWBLANK
	RTS
	
;draw_partisan_menu()
_DRWPRTSN
	JSR _CLRBR
	+__COORD P_PARTISANR, P_PARTISANC  
	+__LAB2XY T_PARTISAN
	JSR _GX_STR
@RESEL
	LDX #$14
	LDY #$16
	JSR _RSELECT
	STA S_PARTISAN
	JSR _FTC
	BNE @RESEL
	JSR _CLRBR
	RTS
	
;right_menu_gray()
;grays out POLL / BUILD HQ if funds are too low or HQ is built that week
_RMENUGRAY
	LDA S_SKIPGAME
	BEQ @SKIPGAME
	RTS
@SKIPGAME

	LDA C_MONEY
	CMP #10
	BCS @GRAYOUT
	LDA T_MENUR+T_MENURLEN-2
	STA T_MENUR+59
	STA T_MENUR+72
	BNE @COLDONE
@GRAYOUT
	LDA T_MENUR+T_MENURLEN-1 
	STA T_MENUR+59
	LDA V_HQBUILT
	BEQ @HQ
	LDA T_MENUR+T_MENURLEN-2
	BNE @HQ2
@HQ
	LDA T_MENUR+T_MENURLEN-1
@HQ2
	STA T_MENUR+72
@COLDONE
	RTS	
	